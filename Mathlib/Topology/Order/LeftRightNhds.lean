/-
Copyright (c) 2017 Johannes HÃ¶lzl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes HÃ¶lzl, Mario Carneiro, Yury Kudryashov
-/
module

public import Mathlib.Order.Filter.AtTopBot.CompleteLattice
public import Mathlib.Order.Filter.AtTopBot.Group
public import Mathlib.Topology.Order.Basic

/-!
# Neighborhoods to the left and to the right on an `OrderTopology`

We've seen some properties of left and right neighborhood of a point in an `OrderClosedTopology`.
In an `OrderTopology`, such neighborhoods can be characterized as the sets containing suitable
intervals to the right or to the left of `a`. We give now these characterizations. -/

public section

open Set Filter TopologicalSpace Topology Function

open OrderDual (toDual ofDual)

variable {Î± Î² Î³ : Type*}

section LinearOrder

variable [TopologicalSpace Î±] [LinearOrder Î±]

section OrderTopology

variable [OrderTopology Î±]

open List in
/-- The following statements are equivalent:

0. `s` is a neighborhood of `a` within `(a, +âˆ)`;
1. `s` is a neighborhood of `a` within `(a, b]`;
2. `s` is a neighborhood of `a` within `(a, b)`;
3. `s` includes `(a, u)` for some `u âˆˆ (a, b]`;
4. `s` includes `(a, u)` for some `u > a`.
-/
theorem TFAE_mem_nhdsGT {a b : Î±} (hab : a < b) (s : Set Î±) :
    TFAE [s âˆˆ ğ“[>] a,
      s âˆˆ ğ“[Ioc a b] a,
      s âˆˆ ğ“[Ioo a b] a,
      âˆƒ u âˆˆ Ioc a b, Ioo a u âŠ† s,
      âˆƒ u âˆˆ Ioi a, Ioo a u âŠ† s] := by
  tfae_have 1 â†” 2 := by
    rw [nhdsWithin_Ioc_eq_nhdsGT hab]
  tfae_have 1 â†” 3 := by
    rw [nhdsWithin_Ioo_eq_nhdsGT hab]
  tfae_have 4 â†’ 5 := fun âŸ¨u, umem, huâŸ© => âŸ¨u, umem.1, huâŸ©
  tfae_have 5 â†’ 1
  | âŸ¨u, hau, huâŸ© => mem_of_superset (Ioo_mem_nhdsGT hau) hu
  tfae_have 1 â†’ 4
  | h => by
    rcases mem_nhdsWithin_iff_exists_mem_nhds_inter.1 h with âŸ¨v, va, hvâŸ©
    rcases exists_Ico_subset_of_mem_nhds' va hab with âŸ¨u, au, huâŸ©
    exact âŸ¨u, au, fun x hx => hv âŸ¨hu âŸ¨le_of_lt hx.1, hx.2âŸ©, hx.1âŸ©âŸ©
  tfae_finish

theorem mem_nhdsGT_iff_exists_mem_Ioc_Ioo_subset {a u' : Î±} {s : Set Î±} (hu' : a < u') :
    s âˆˆ ğ“[>] a â†” âˆƒ u âˆˆ Ioc a u', Ioo a u âŠ† s :=
  (TFAE_mem_nhdsGT hu' s).out 0 3

/-- A set is a neighborhood of `a` within `(a, +âˆ)` if and only if it contains an interval `(a, u)`
with `a < u < u'`, provided `a` is not a top element. -/
theorem mem_nhdsGT_iff_exists_Ioo_subset' {a u' : Î±} {s : Set Î±} (hu' : a < u') :
    s âˆˆ ğ“[>] a â†” âˆƒ u âˆˆ Ioi a, Ioo a u âŠ† s :=
  (TFAE_mem_nhdsGT hu' s).out 0 4

theorem nhdsGT_basis_of_exists_gt {a : Î±} (h : âˆƒ b, a < b) : (ğ“[>] a).HasBasis (a < Â·) (Ioo a) :=
  let âŸ¨_, hâŸ© := h
  âŸ¨fun _ => mem_nhdsGT_iff_exists_Ioo_subset' hâŸ©

lemma nhdsGT_basis [NoMaxOrder Î±] (a : Î±) : (ğ“[>] a).HasBasis (a < Â·) (Ioo a) :=
  nhdsGT_basis_of_exists_gt <| exists_gt a

theorem eventually_nhdsGT_iff [NoMaxOrder Î±] {a : Î±} {p : Î± â†’ Prop} :
    (âˆ€á¶  t in ğ“[>] a, p t) â†” âˆƒ u, a < u âˆ§ âˆ€ t, a < t âˆ§ t < u â†’ p t := by
  refine (nhdsGT_basis a).eventually_iff.trans ?_
  constructor
  Â· rintro âŸ¨u, hau, hâŸ©
    refine âŸ¨u, hau, ?_âŸ©
    intro t ht
    exact h âŸ¨ht.1, ht.2âŸ©
  Â· rintro âŸ¨u, hau, hâŸ©
    refine âŸ¨u, hau, ?_âŸ©
    intro t ht
    exact h t ht

theorem frequently_nhdsGT_iff [NoMaxOrder Î±] {a : Î±} {p : Î± â†’ Prop} :
    (âˆƒá¶  t in ğ“[>] a, p t) â†” âˆ€ u, a < u â†’ âˆƒ t, a < t âˆ§ t < u âˆ§ p t := by
  refine (nhdsGT_basis a).frequently_iff.trans ?_
  constructor
  Â· intro h u hu
    rcases h u hu with âŸ¨t, ht, hpâŸ©
    exact âŸ¨t, ht.1, ht.2, hpâŸ©
  Â· intro h u hu
    rcases h u hu with âŸ¨t, ht1, ht2, hpâŸ©
    exact âŸ¨t, âŸ¨ht1, ht2âŸ©, hpâŸ©

theorem nhdsGT_eq_bot_iff {a : Î±} : ğ“[>] a = âŠ¥ â†” IsTop a âˆ¨ âˆƒ b, a â‹– b := by
  by_cases ha : IsTop a
  Â· simp [ha, ha.isMax.Ioi_eq]
  Â· simp only [ha, false_or]
    rw [isTop_iff_isMax, not_isMax_iff] at ha
    simp only [(nhdsGT_basis_of_exists_gt ha).eq_bot_iff, covBy_iff_Ioo_eq]

/-- A set is a neighborhood of `a` within `(a, +âˆ)` if and only if it contains an interval `(a, u)`
with `a < u`. -/
theorem mem_nhdsGT_iff_exists_Ioo_subset [NoMaxOrder Î±] {a : Î±} {s : Set Î±} :
    s âˆˆ ğ“[>] a â†” âˆƒ u âˆˆ Ioi a, Ioo a u âŠ† s :=
  let âŸ¨_u', hu'âŸ© := exists_gt a
  mem_nhdsGT_iff_exists_Ioo_subset' hu'

/-- The set of points which are isolated on the right is countable when the space is
second-countable. -/
theorem countable_setOf_isolated_right [SecondCountableTopology Î±] :
    { x : Î± | ğ“[>] x = âŠ¥ }.Countable := by
  simp only [nhdsGT_eq_bot_iff, setOf_or]
  exact (subsingleton_isTop Î±).countable.union countable_setOf_covBy_right

/-- The set of points which are isolated on the left is countable when the space is
second-countable. -/
theorem countable_setOf_isolated_left [SecondCountableTopology Î±] :
    { x : Î± | ğ“[<] x = âŠ¥ }.Countable :=
  countable_setOf_isolated_right (Î± := Î±áµ’áµˆ)

/-- The set of points in a set which are isolated on the right in this set is countable when the
space is second-countable. -/
theorem countable_setOf_isolated_right_within [SecondCountableTopology Î±] {s : Set Î±} :
    { x âˆˆ s | ğ“[s âˆ© Ioi x] x = âŠ¥ }.Countable := by
  /- This does not follow from `countable_setOf_isolated_right`, which gives the result when `s`
  is the whole space, as one cannot use it inside the subspace since it doesn't have the order
  topology. Instead, we follow the main steps of its proof. -/
  let t := { x âˆˆ s | ğ“[s âˆ© Ioi x] x = âŠ¥ âˆ§ Â¬ IsTop x}
  suffices H : t.Countable by
    have : { x âˆˆ s | ğ“[s âˆ© Ioi x] x = âŠ¥ } âŠ† t âˆª {x | IsTop x} := by
      intro x hx
      by_cases h'x : IsTop x
      Â· simp [h'x]
      Â· simpa [-sep_and, t, h'x]
    apply Countable.mono this
    simp [H, (subsingleton_isTop Î±).countable]
  have (x) (hx : x âˆˆ t) : âˆƒ y > x, s âˆ© Ioo x y = âˆ… := by
    simp only [â† empty_mem_iff_bot, mem_nhdsWithin_iff_exists_mem_nhds_inter,
      subset_empty_iff, IsTop, not_forall, not_le, mem_setOf_eq, t] at hx
    rcases hx.2.1 with âŸ¨u, hu, h'uâŸ©
    obtain âŸ¨y, hxy, hyâŸ© : âˆƒ y, x < y âˆ§ Ico x y âŠ† u := exists_Ico_subset_of_mem_nhds hu hx.2.2
    refine âŸ¨y, hxy, ?_âŸ©
    contrapose! h'u
    apply h'u.mono
    intro z hz
    exact âŸ¨hy âŸ¨hz.2.1.le, hz.2.2âŸ©, hz.1, hz.2.1âŸ©
  choose! y hy h'y using this
  apply Set.PairwiseDisjoint.countable_of_Ioo (y := y) _ hy
  simp only [PairwiseDisjoint, Set.Pairwise, Function.onFun]
  intro a ha b hb hab
  wlog! H : a < b generalizing a b with h
  Â· have : b < a := lt_of_le_of_ne H hab.symm
    exact (h hb ha hab.symm this).symm
  have : y a â‰¤ b := by
    by_contra!
    have : b âˆˆ s âˆ© Ioo a (y a) := by simp [hb.1, H, this]
    simp [h'y a ha] at this
  rw [disjoint_iff_forall_ne]
  exact fun u hu v hv â†¦ ((hu.2.trans_le this).trans hv.1).ne

/-- The set of points in a set which are isolated on the left in this set is countable when the
space is second-countable. -/
theorem countable_setOf_isolated_left_within [SecondCountableTopology Î±] {s : Set Î±} :
    { x âˆˆ s | ğ“[s âˆ© Iio x] x = âŠ¥ }.Countable :=
  countable_setOf_isolated_right_within (Î± := Î±áµ’áµˆ)

/-- A set is a neighborhood of `a` within `(a, +âˆ)` if and only if it contains an interval `(a, u]`
with `a < u`. -/
theorem mem_nhdsGT_iff_exists_Ioc_subset [NoMaxOrder Î±] [DenselyOrdered Î±] {a : Î±} {s : Set Î±} :
    s âˆˆ ğ“[>] a â†” âˆƒ u âˆˆ Ioi a, Ioc a u âŠ† s := by
  rw [mem_nhdsGT_iff_exists_Ioo_subset]
  constructor
  Â· rintro âŸ¨u, au, asâŸ©
    rcases exists_between au with âŸ¨v, hvâŸ©
    exact âŸ¨v, hv.1, fun x hx => as âŸ¨hx.1, lt_of_le_of_lt hx.2 hv.2âŸ©âŸ©
  Â· rintro âŸ¨u, au, asâŸ©
    exact âŸ¨u, au, Subset.trans Ioo_subset_Ioc_self asâŸ©

open List in
/-- The following statements are equivalent:

0. `s` is a neighborhood of `b` within `(-âˆ, b)`
1. `s` is a neighborhood of `b` within `[a, b)`
2. `s` is a neighborhood of `b` within `(a, b)`
3. `s` includes `(l, b)` for some `l âˆˆ [a, b)`
4. `s` includes `(l, b)` for some `l < b` -/
theorem TFAE_mem_nhdsLT {a b : Î±} (h : a < b) (s : Set Î±) :
    TFAE [s âˆˆ ğ“[<] b, -- 0 : `s` is a neighborhood of `b` within `(-âˆ, b)`
        s âˆˆ ğ“[Ico a b] b, -- 1 : `s` is a neighborhood of `b` within `[a, b)`
        s âˆˆ ğ“[Ioo a b] b, -- 2 : `s` is a neighborhood of `b` within `(a, b)`
        âˆƒ l âˆˆ Ico a b, Ioo l b âŠ† s, -- 3 : `s` includes `(l, b)` for some `l âˆˆ [a, b)`
        âˆƒ l âˆˆ Iio b, Ioo l b âŠ† s] := by -- 4 : `s` includes `(l, b)` for some `l < b`
  simpa using TFAE_mem_nhdsGT h.dual (ofDual â»Â¹' s)

theorem mem_nhdsLT_iff_exists_mem_Ico_Ioo_subset {a l' : Î±} {s : Set Î±} (hl' : l' < a) :
    s âˆˆ ğ“[<] a â†” âˆƒ l âˆˆ Ico l' a, Ioo l a âŠ† s :=
  (TFAE_mem_nhdsLT hl' s).out 0 3

/-- A set is a neighborhood of `a` within `(-âˆ, a)` if and only if it contains an interval `(l, a)`
with `l < a`, provided `a` is not a bottom element. -/
theorem mem_nhdsLT_iff_exists_Ioo_subset' {a l' : Î±} {s : Set Î±} (hl' : l' < a) :
    s âˆˆ ğ“[<] a â†” âˆƒ l âˆˆ Iio a, Ioo l a âŠ† s :=
  (TFAE_mem_nhdsLT hl' s).out 0 4

/-- A set is a neighborhood of `a` within `(-âˆ, a)` if and only if it contains an interval `(l, a)`
with `l < a`. -/
theorem mem_nhdsLT_iff_exists_Ioo_subset [NoMinOrder Î±] {a : Î±} {s : Set Î±} :
    s âˆˆ ğ“[<] a â†” âˆƒ l âˆˆ Iio a, Ioo l a âŠ† s :=
  let âŸ¨_, hâŸ© := exists_lt a
  mem_nhdsLT_iff_exists_Ioo_subset' h

/-- A set is a neighborhood of `a` within `(-âˆ, a)` if and only if it contains an interval `[l, a)`
with `l < a`. -/
theorem mem_nhdsLT_iff_exists_Ico_subset [NoMinOrder Î±] [DenselyOrdered Î±] {a : Î±} {s : Set Î±} :
    s âˆˆ ğ“[<] a â†” âˆƒ l âˆˆ Iio a, Ico l a âŠ† s := by
  have : ofDual â»Â¹' s âˆˆ ğ“[>] toDual a â†” _ := mem_nhdsGT_iff_exists_Ioc_subset
  simpa using this

theorem nhdsLT_basis_of_exists_lt {a : Î±} (h : âˆƒ b, b < a) : (ğ“[<] a).HasBasis (Â· < a) (Ioo Â· a) :=
  let âŸ¨_, hâŸ© := h
  âŸ¨fun _ => mem_nhdsLT_iff_exists_Ioo_subset' hâŸ©

theorem nhdsLT_basis [NoMinOrder Î±] (a : Î±) : (ğ“[<] a).HasBasis (Â· < a) (Ioo Â· a) :=
  nhdsLT_basis_of_exists_lt <| exists_lt a

theorem eventually_nhdsLT_iff [NoMinOrder Î±] {a : Î±} {p : Î± â†’ Prop} :
    (âˆ€á¶  t in ğ“[<] a, p t) â†” âˆƒ l, l < a âˆ§ âˆ€ t, l < t âˆ§ t < a â†’ p t := by
  refine (nhdsLT_basis a).eventually_iff.trans ?_
  constructor
  Â· rintro âŸ¨l, hla, hâŸ©
    refine âŸ¨l, hla, ?_âŸ©
    intro t ht
    exact h âŸ¨ht.1, ht.2âŸ©
  Â· rintro âŸ¨l, hla, hâŸ©
    refine âŸ¨l, hla, ?_âŸ©
    intro t ht
    exact h t ht

theorem frequently_nhdsLT_iff [NoMinOrder Î±] {a : Î±} {p : Î± â†’ Prop} :
    (âˆƒá¶  t in ğ“[<] a, p t) â†” âˆ€ l, l < a â†’ âˆƒ t, l < t âˆ§ t < a âˆ§ p t := by
  refine (nhdsLT_basis a).frequently_iff.trans ?_
  constructor
  Â· intro h l hl
    rcases h l hl with âŸ¨t, ht, hpâŸ©
    exact âŸ¨t, ht.1, ht.2, hpâŸ©
  Â· intro h l hl
    rcases h l hl with âŸ¨t, ht1, ht2, hpâŸ©
    exact âŸ¨t, âŸ¨ht1, ht2âŸ©, hpâŸ©

theorem nhdsLT_eq_bot_iff {a : Î±} : ğ“[<] a = âŠ¥ â†” IsBot a âˆ¨ âˆƒ b, b â‹– a := by
  convert (config := { preTransparency := .default }) nhdsGT_eq_bot_iff (a := OrderDual.toDual a)
    using 4
  exact ofDual_covBy_ofDual_iff

open List in
/-- The following statements are equivalent:

0. `s` is a neighborhood of `a` within `[a, +âˆ)`;
1. `s` is a neighborhood of `a` within `[a, b]`;
2. `s` is a neighborhood of `a` within `[a, b)`;
3. `s` includes `[a, u)` for some `u âˆˆ (a, b]`;
4. `s` includes `[a, u)` for some `u > a`.
-/
theorem TFAE_mem_nhdsGE {a b : Î±} (hab : a < b) (s : Set Î±) :
    TFAE [s âˆˆ ğ“[â‰¥] a,
      s âˆˆ ğ“[Icc a b] a,
      s âˆˆ ğ“[Ico a b] a,
      âˆƒ u âˆˆ Ioc a b, Ico a u âŠ† s,
      âˆƒ u âˆˆ Ioi a, Ico a u âŠ† s] := by
  tfae_have 1 â†” 2 := by
    rw [nhdsWithin_Icc_eq_nhdsGE hab]
  tfae_have 1 â†” 3 := by
    rw [nhdsWithin_Ico_eq_nhdsGE hab]
  tfae_have 1 â†” 5 := (nhdsGE_basis_of_exists_gt âŸ¨b, habâŸ©).mem_iff
  tfae_have 4 â†’ 5 := fun âŸ¨u, umem, huâŸ© => âŸ¨u, umem.1, huâŸ©
  tfae_have 5 â†’ 4
  | âŸ¨u, hua, husâŸ© => âŸ¨min u b, âŸ¨lt_min hua hab, min_le_right _ _âŸ©,
      (Ico_subset_Ico_right <| min_le_left _ _).trans husâŸ©
  tfae_finish

theorem mem_nhdsGE_iff_exists_mem_Ioc_Ico_subset {a u' : Î±} {s : Set Î±} (hu' : a < u') :
    s âˆˆ ğ“[â‰¥] a â†” âˆƒ u âˆˆ Ioc a u', Ico a u âŠ† s :=
  (TFAE_mem_nhdsGE hu' s).out 0 3 (by simp) (by simp)

/-- A set is a neighborhood of `a` within `[a, +âˆ)` if and only if it contains an interval `[a, u)`
with `a < u < u'`, provided `a` is not a top element. -/
theorem mem_nhdsGE_iff_exists_Ico_subset' {a u' : Î±} {s : Set Î±} (hu' : a < u') :
    s âˆˆ ğ“[â‰¥] a â†” âˆƒ u âˆˆ Ioi a, Ico a u âŠ† s :=
  (TFAE_mem_nhdsGE hu' s).out 0 4 (by simp) (by simp)

/-- A set is a neighborhood of `a` within `[a, +âˆ)` if and only if it contains an interval `[a, u)`
with `a < u`. -/
theorem mem_nhdsGE_iff_exists_Ico_subset [NoMaxOrder Î±] {a : Î±} {s : Set Î±} :
    s âˆˆ ğ“[â‰¥] a â†” âˆƒ u âˆˆ Ioi a, Ico a u âŠ† s :=
  let âŸ¨_, hu'âŸ© := exists_gt a
  mem_nhdsGE_iff_exists_Ico_subset' hu'

theorem nhdsGE_basis_Ico [NoMaxOrder Î±] (a : Î±) : (ğ“[â‰¥] a).HasBasis (fun u => a < u) (Ico a) :=
  âŸ¨fun _ => mem_nhdsGE_iff_exists_Ico_subsetâŸ©

/-- The filter of right neighborhoods has a basis of closed intervals. -/
theorem nhdsGE_basis_Icc [NoMaxOrder Î±] [DenselyOrdered Î±] {a : Î±} :
    (ğ“[â‰¥] a).HasBasis (a < Â·) (Icc a) :=
  (nhdsGE_basis _).to_hasBasis
    (fun _u hu â†¦ (exists_between hu).imp fun _v hv â†¦ hv.imp_right Icc_subset_Ico_right) fun u hu â†¦
    âŸ¨u, hu, Ico_subset_Icc_selfâŸ©

/-- A set is a neighborhood of `a` within `[a, +âˆ)` if and only if it contains an interval `[a, u]`
with `a < u`. -/
theorem mem_nhdsGE_iff_exists_Icc_subset [NoMaxOrder Î±] [DenselyOrdered Î±] {a : Î±} {s : Set Î±} :
    s âˆˆ ğ“[â‰¥] a â†” âˆƒ u, a < u âˆ§ Icc a u âŠ† s :=
  nhdsGE_basis_Icc.mem_iff

open List in
/-- The following statements are equivalent:

0. `s` is a neighborhood of `b` within `(-âˆ, b]`
1. `s` is a neighborhood of `b` within `[a, b]`
2. `s` is a neighborhood of `b` within `(a, b]`
3. `s` includes `(l, b]` for some `l âˆˆ [a, b)`
4. `s` includes `(l, b]` for some `l < b` -/
theorem TFAE_mem_nhdsLE {a b : Î±} (h : a < b) (s : Set Î±) :
    TFAE [s âˆˆ ğ“[â‰¤] b, -- 0 : `s` is a neighborhood of `b` within `(-âˆ, b]`
      s âˆˆ ğ“[Icc a b] b, -- 1 : `s` is a neighborhood of `b` within `[a, b]`
      s âˆˆ ğ“[Ioc a b] b, -- 2 : `s` is a neighborhood of `b` within `(a, b]`
      âˆƒ l âˆˆ Ico a b, Ioc l b âŠ† s, -- 3 : `s` includes `(l, b]` for some `l âˆˆ [a, b)`
      âˆƒ l âˆˆ Iio b, Ioc l b âŠ† s] := by -- 4 : `s` includes `(l, b]` for some `l < b`
  simpa using TFAE_mem_nhdsGE h.dual (ofDual â»Â¹' s)

theorem mem_nhdsLE_iff_exists_mem_Ico_Ioc_subset {a l' : Î±} {s : Set Î±} (hl' : l' < a) :
    s âˆˆ ğ“[â‰¤] a â†” âˆƒ l âˆˆ Ico l' a, Ioc l a âŠ† s :=
  (TFAE_mem_nhdsLE hl' s).out 0 3 (by simp) (by simp)

/-- A set is a neighborhood of `a` within `(-âˆ, a]` if and only if it contains an interval `(l, a]`
with `l < a`, provided `a` is not a bottom element. -/
theorem mem_nhdsLE_iff_exists_Ioc_subset' {a l' : Î±} {s : Set Î±} (hl' : l' < a) :
    s âˆˆ ğ“[â‰¤] a â†” âˆƒ l âˆˆ Iio a, Ioc l a âŠ† s :=
  (TFAE_mem_nhdsLE hl' s).out 0 4 (by simp) (by simp)

/-- A set is a neighborhood of `a` within `(-âˆ, a]` if and only if it contains an interval `(l, a]`
with `l < a`. -/
theorem mem_nhdsLE_iff_exists_Ioc_subset [NoMinOrder Î±] {a : Î±} {s : Set Î±} :
    s âˆˆ ğ“[â‰¤] a â†” âˆƒ l âˆˆ Iio a, Ioc l a âŠ† s :=
  let âŸ¨_, hl'âŸ© := exists_lt a
  mem_nhdsLE_iff_exists_Ioc_subset' hl'

/-- A set is a neighborhood of `a` within `(-âˆ, a]` if and only if it contains an interval `[l, a]`
with `l < a`. -/
theorem mem_nhdsLE_iff_exists_Icc_subset [NoMinOrder Î±] [DenselyOrdered Î±] {a : Î±}
    {s : Set Î±} : s âˆˆ ğ“[â‰¤] a â†” âˆƒ l, l < a âˆ§ Icc l a âŠ† s :=
  calc s âˆˆ ğ“[â‰¤] a â†” ofDual â»Â¹' s âˆˆ ğ“[â‰¥] (toDual a) := Iff.rfl
  _ â†” âˆƒ u : Î±, toDual a < toDual u âˆ§ Icc (toDual a) (toDual u) âŠ† ofDual â»Â¹' s :=
    mem_nhdsGE_iff_exists_Icc_subset
  _ â†” âˆƒ l, l < a âˆ§ Icc l a âŠ† s := by simp

/-- The filter of left neighborhoods has a basis of closed intervals. -/
theorem nhdsLE_basis_Icc [NoMinOrder Î±] [DenselyOrdered Î±] {a : Î±} :
    (ğ“[â‰¤] a).HasBasis (Â· < a) (Icc Â· a) :=
  âŸ¨fun _ â†¦ mem_nhdsLE_iff_exists_Icc_subsetâŸ©

end OrderTopology

end LinearOrder

section LinearOrderedCommGroup

variable [TopologicalSpace Î±] [CommGroup Î±] [LinearOrder Î±] [IsOrderedMonoid Î±]
  [OrderTopology Î±]
variable {l : Filter Î²} {f g : Î² â†’ Î±}

@[to_additive]
theorem nhds_eq_iInf_mabs_div (a : Î±) : ğ“ a = â¨… r > 1, ğ“Ÿ { b | |a / b|â‚˜ < r } := by
  simp only [nhds_eq_order, mabs_lt, setOf_and, â† inf_principal, iInf_inf_eq]
  refine (congr_argâ‚‚ _ ?_ ?_).trans (inf_comm ..)
  Â· refine (Equiv.divLeft a).iInf_congr fun x => ?_; simp [Ioi]
  Â· refine (Equiv.divRight a).iInf_congr fun x => ?_; simp [Iio]

@[to_additive]
theorem orderTopology_of_nhds_mabs {Î± : Type*} [TopologicalSpace Î±] [CommGroup Î±] [LinearOrder Î±]
    [IsOrderedMonoid Î±]
    (h_nhds : âˆ€ a : Î±, ğ“ a = â¨… r > 1, ğ“Ÿ { b | |a / b|â‚˜ < r }) : OrderTopology Î± := by
  refine âŸ¨TopologicalSpace.ext_nhds fun a => ?_âŸ©
  rw [h_nhds]
  letI := Preorder.topology Î±; letI : OrderTopology Î± := âŸ¨rflâŸ©
  exact (nhds_eq_iInf_mabs_div a).symm

@[to_additive]
theorem LinearOrderedCommGroup.tendsto_nhds {x : Filter Î²} {a : Î±} :
    Tendsto f x (ğ“ a) â†” âˆ€ Îµ > (1 : Î±), âˆ€á¶  b in x, |f b / a|â‚˜ < Îµ := by
  simp [nhds_eq_iInf_mabs_div, mabs_div_comm a]

@[to_additive]
theorem eventually_mabs_div_lt (a : Î±) {Îµ : Î±} (hÎµ : 1 < Îµ) : âˆ€á¶  x in ğ“ a, |x / a|â‚˜ < Îµ :=
  (nhds_eq_iInf_mabs_div a).symm â–¸
    mem_iInf_of_mem Îµ (mem_iInf_of_mem hÎµ <| by simp only [mabs_div_comm, mem_principal_self])

/-- In a linearly ordered commutative group with the order topology,
if `f` tends to `C` and `g` tends to `atTop` then `f * g` tends to `atTop`. -/
@[to_additive add_atTop /-- In a linearly ordered additive commutative group with the order
topology, if `f` tends to `C` and `g` tends to `atTop` then `f + g` tends to `atTop`. -/]
theorem Filter.Tendsto.mul_atTop' {C : Î±} (hf : Tendsto f l (ğ“ C)) (hg : Tendsto g l atTop) :
    Tendsto (fun x => f x * g x) l atTop := by
  nontriviality Î±
  obtain âŸ¨C', hC'âŸ© : âˆƒ C', C' < C := exists_lt C
  refine tendsto_atTop_mul_left_of_le' _ C' ?_ hg
  exact (hf.eventually (lt_mem_nhds hC')).mono fun x => le_of_lt

/-- In a linearly ordered commutative group with the order topology,
if `f` tends to `C` and `g` tends to `atBot` then `f * g` tends to `atBot`. -/
@[to_additive add_atBot /-- In a linearly ordered additive commutative group with the order
topology, if `f` tends to `C` and `g` tends to `atBot` then `f + g` tends to `atBot`. -/]
theorem Filter.Tendsto.mul_atBot' {C : Î±} (hf : Tendsto f l (ğ“ C)) (hg : Tendsto g l atBot) :
    Tendsto (fun x => f x * g x) l atBot :=
  Filter.Tendsto.mul_atTop' (Î± := Î±áµ’áµˆ) hf hg

/-- In a linearly ordered commutative group with the order topology,
if `f` tends to `atTop` and `g` tends to `C` then `f * g` tends to `atTop`. -/
@[to_additive atTop_add /-- In a linearly ordered additive commutative group with the order
topology, if `f` tends to `atTop` and `g` tends to `C` then `f + g` tends to `atTop`. -/]
theorem Filter.Tendsto.atTop_mul' {C : Î±} (hf : Tendsto f l atTop) (hg : Tendsto g l (ğ“ C)) :
    Tendsto (fun x => f x * g x) l atTop := by
  conv in _ * _ => rw [mul_comm]
  exact hg.mul_atTop' hf

/-- In a linearly ordered commutative group with the order topology,
if `f` tends to `atBot` and `g` tends to `C` then `f * g` tends to `atBot`. -/
@[to_additive atBot_add /-- In a linearly ordered additive commutative group with the order
topology, if `f` tends to `atBot` and `g` tends to `C` then `f + g` tends to `atBot`. -/]
theorem Filter.Tendsto.atBot_mul' {C : Î±} (hf : Tendsto f l atBot) (hg : Tendsto g l (ğ“ C)) :
    Tendsto (fun x => f x * g x) l atBot := by
  conv in _ * _ => rw [mul_comm]
  exact hg.mul_atBot' hf

@[to_additive]
theorem nhds_basis_mabs_div_lt [NoMaxOrder Î±] (a : Î±) :
    (ğ“ a).HasBasis (fun Îµ : Î± => (1 : Î±) < Îµ) fun Îµ => { b | |b / a|â‚˜ < Îµ } := by
  simp only [nhds_eq_iInf_mabs_div, mabs_div_comm (a := a)]
  refine hasBasis_biInf_principal' (fun x hx y hy => ?_) (exists_gt _)
  exact âŸ¨min x y, lt_min hx hy, fun _ hz => hz.trans_le (min_le_left _ _),
    fun _ hz => hz.trans_le (min_le_right _ _)âŸ©

@[to_additive]
theorem nhds_basis_Ioo_one_lt [NoMaxOrder Î±] (a : Î±) :
    (ğ“ a).HasBasis (fun Îµ : Î± => (1 : Î±) < Îµ) fun Îµ => Ioo (a / Îµ) (a * Îµ) := by
  convert nhds_basis_mabs_div_lt a
  simp only [Ioo, mabs_lt, â† div_lt_iff_lt_mul, inv_lt_div_iff_lt_mul, div_lt_comm]

@[to_additive]
theorem nhds_basis_Icc_one_lt [NoMaxOrder Î±] [DenselyOrdered Î±] (a : Î±) :
    (ğ“ a).HasBasis ((1 : Î±) < Â·) fun Îµ â†¦ Icc (a / Îµ) (a * Îµ) :=
  (nhds_basis_Ioo_one_lt a).to_hasBasis
    (fun _Îµ Îµâ‚ â†¦ let âŸ¨Î´, Î´â‚, Î´ÎµâŸ© := exists_between Îµâ‚
      âŸ¨Î´, Î´â‚, Icc_subset_Ioo (by gcongr) (by gcongr)âŸ©)
    (fun Îµ Îµâ‚ â†¦ âŸ¨Îµ, Îµâ‚, Ioo_subset_Icc_selfâŸ©)

variable (Î±) in
@[to_additive]
theorem nhds_basis_one_mabs_lt [NoMaxOrder Î±] :
    (ğ“ (1 : Î±)).HasBasis (fun Îµ : Î± => (1 : Î±) < Îµ) fun Îµ => { b | |b|â‚˜ < Îµ } := by
  simpa using nhds_basis_mabs_div_lt (1 : Î±)

/-- If `a > 1`, then open intervals `(a / Îµ, aÎµ)`, `1 < Îµ â‰¤ a`,
form a basis of neighborhoods of `a`.

This upper bound for `Îµ` guarantees that all elements of these intervals are greater than one. -/
@[to_additive /-- If `a` is positive, then the intervals `(a - Îµ, a + Îµ)`, `0 < Îµ â‰¤ a`,
form a basis of neighborhoods of `a`.

This upper bound for `Îµ` guarantees that all elements of these intervals are positive. -/]
theorem nhds_basis_Ioo_one_lt_of_one_lt [NoMaxOrder Î±] {a : Î±} (ha : 1 < a) :
    (ğ“ a).HasBasis (fun Îµ : Î± => (1 : Î±) < Îµ âˆ§ Îµ â‰¤ a) fun Îµ => Ioo (a / Îµ) (a * Îµ) :=
  (nhds_basis_Ioo_one_lt a).restrict fun Îµ hÎµ â†¦
    âŸ¨min a Îµ, lt_min ha hÎµ, min_le_left _ _, by gcongr <;> apply min_le_rightâŸ©

end LinearOrderedCommGroup

namespace Set.OrdConnected

section ClosedIciTopology

variable [TopologicalSpace Î±] [LinearOrder Î±] [ClosedIciTopology Î±] {S : Set Î±} {x y : Î±}

/-- If `S` is order-connected and contains two points `x < y`,
then `S` is a right neighbourhood of `x`. -/
lemma mem_nhdsGE (hS : OrdConnected S) (hx : x âˆˆ S) (hy : y âˆˆ S) (hxy : x < y) : S âˆˆ ğ“[â‰¥] x :=
  mem_of_superset (Icc_mem_nhdsGE hxy) <| hS.out hx hy

/-- If `S` is order-connected and contains two points `x < y`,
then `S` is a punctured right neighbourhood of `x`. -/
lemma mem_nhdsGT (hS : OrdConnected S) (hx : x âˆˆ S) (hy : y âˆˆ S) (hxy : x < y) : S âˆˆ ğ“[>] x :=
  nhdsWithin_mono _ Ioi_subset_Ici_self <| hS.mem_nhdsGE hx hy hxy

end ClosedIciTopology

variable [TopologicalSpace Î±] [LinearOrder Î±] [ClosedIicTopology Î±] {S : Set Î±} {x y : Î±}

/-- If `S` is order-connected and contains two points `x < y`, then `S` is a left neighbourhood
of `y`. -/
lemma mem_nhdsLE (hS : OrdConnected S) (hx : x âˆˆ S) (hy : y âˆˆ S) (hxy : x < y) : S âˆˆ ğ“[â‰¤] y :=
  hS.dual.mem_nhdsGE hy hx hxy

/-- If `S` is order-connected and contains two points `x < y`, then `S` is a punctured left
neighbourhood of `y`. -/
lemma mem_nhdsLT (hS : OrdConnected S) (hx : x âˆˆ S) (hy : y âˆˆ S) (hxy : x < y) : S âˆˆ ğ“[<] y :=
  hS.dual.mem_nhdsGT hy hx hxy

end OrdConnected

end Set
