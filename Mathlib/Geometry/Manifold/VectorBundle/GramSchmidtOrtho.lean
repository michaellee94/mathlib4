/-
Copyright (c) 2025 Michael Rothgang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Patrick Massot, Michael Rothgang
-/
import Mathlib.Analysis.InnerProductSpace.GramSchmidtOrtho
import Mathlib.Analysis.SpecialFunctions.Sqrt
import Mathlib.Geometry.Manifold.VectorBundle.Riemannian
import Mathlib.Geometry.Manifold.VectorBundle.SmoothSection
import Mathlib.Geometry.Manifold.Elaborators

/-!
# Gram-Schmidt orthonormalization on sections of Riemannian vector bundles

In this file, we provide a version of the Gram-Schmidt orthonormalization procedure
for sections of Riemannian vector bundles: this produces a system of sections which are orthogonal
with respect to the bundle metric. If the initial sections were linearly independent (resp.
formed a basis) at the point, so do the normalized sections.

If the bundle metric is `C^k`, then the procedure preserves regularity of sections:
if all sections are `C^k`, so are their normalized versions.

This will be used in `OrthonormalFrame.lean` to convert a local frame to a local orthonormal frame.

## Tags
vector bundle, bundle metric, orthonormal frame, Gram-Schmidt

-/

open Manifold Bundle Module
open scoped ContDiff Topology

-- Let `E` be a topological vector bundle over a topological space `B`,
-- with a continuous Riemannian structure.
variable {B F : Type*} [TopologicalSpace B]
  [NormedAddCommGroup F] [NormedSpace â„ F]
  {E : B â†’ Type*} [TopologicalSpace (TotalSpace F E)] [âˆ€ x, NormedAddCommGroup (E x)]
  [âˆ€ x, InnerProductSpace â„ (E x)] [FiberBundle F E] [VectorBundle â„ F E]
  [IsContinuousRiemannianBundle F E]

variable {Î¹ : Type*} [LinearOrder Î¹] [LocallyFiniteOrderBot Î¹] [WellFoundedLT Î¹]

local notation "âŸª" x ", " y "âŸ«" => inner â„ x y

open Finset Submodule

namespace VectorBundle

/-- The Gram-Schmidt process takes a set of sections as input
and outputs a set of sections which are point-wise orthogonal and have the same span.
Basically, we apply the Gram-Schmidt algorithm point-wise. -/
noncomputable def gramSchmidt (s : Î¹ â†’ (x : B) â†’ E x) (n : Î¹) : (x : B) â†’ E x :=
  fun x â†¦ InnerProductSpace.gramSchmidt â„ (s Â· x) n

-- Let `s i` be a collection of sections in `E`, indexed by `Î¹`.
variable {s : Î¹ â†’ (x : B) â†’ E x}

omit [TopologicalSpace B]

/-- This lemma uses `âˆ‘ i in` instead of `âˆ‘ i :`. -/
theorem gramSchmidt_def (s : Î¹ â†’ (x : B) â†’ E x) (n : Î¹) (x) :
    gramSchmidt s n x =
      s n x - âˆ‘ i âˆˆ Iio n, (â„ âˆ™ gramSchmidt s i x).starProjection (s n x) := by
  rw [gramSchmidt, InnerProductSpace.gramSchmidt_def]
  congr

theorem gramSchmidt_def' (s : Î¹ â†’ (x : B) â†’ E x) (n : Î¹) (x) :
    s n x = gramSchmidt s n x +
      âˆ‘ i âˆˆ Iio n, (â„ âˆ™ gramSchmidt s i x).starProjection (s n x) := by
  rw [gramSchmidt_def, sub_add_cancel]

theorem gramSchmidt_def'' (s : Î¹ â†’ (x : B) â†’ E x) (n : Î¹) (x) :
    s n x = gramSchmidt s n x + âˆ‘ i âˆˆ Iio n,
      (âŸªgramSchmidt s i x, s n xâŸ« / (â€–gramSchmidt s i xâ€–) ^ 2) â€¢ gramSchmidt s i x :=
  InnerProductSpace.gramSchmidt_def'' â„ (s Â· x) n

@[simp]
lemma gramSchmidt_apply (s : Î¹ â†’ (x : B) â†’ E x) (n : Î¹) (x) :
    gramSchmidt s n x = InnerProductSpace.gramSchmidt â„ (s Â· x) n := rfl

@[simp]
theorem gramSchmidt_bot {Î¹ : Type*} [LinearOrder Î¹] [LocallyFiniteOrder Î¹] [OrderBot Î¹]
    [WellFoundedLT Î¹] (s : Î¹ â†’ (x : B) â†’ E x) : gramSchmidt s âŠ¥ = s âŠ¥ := by
  ext x
  apply InnerProductSpace.gramSchmidt_bot

@[simp]
theorem gramSchmidt_zero (n : Î¹) : gramSchmidt (0 : Î¹ â†’ (x : B) â†’ E x) n = 0 := by
  ext x
  simpa using InnerProductSpace.gramSchmidt_zero ..

/-- **Gram-Schmidt Orthogonalisation**: `gramSchmidt` produces a point-wise orthogonal system
of sections. -/
theorem gramSchmidt_orthogonal (s : Î¹ â†’ (x : B) â†’ E x) {a b : Î¹} (hâ‚€ : a â‰  b) (x) :
    âŸªgramSchmidt s a x, gramSchmidt s b xâŸ« = 0 :=
  InnerProductSpace.gramSchmidt_orthogonal _ _ hâ‚€

/-- This is another version of `gramSchmidt_orthogonal` using `Pairwise` instead. -/
theorem gramSchmidt_pairwise_orthogonal (s : Î¹ â†’ (x : B) â†’ E x) (x) :
    Pairwise fun a b â†¦ âŸªgramSchmidt s a x, gramSchmidt s b xâŸ« = 0 :=
  fun _ _ h â†¦ gramSchmidt_orthogonal s h _

theorem gramSchmidt_inv_triangular (s : Î¹ â†’ (x : B) â†’ E x) {i j : Î¹} (hij : i < j) (x) :
    âŸªgramSchmidt s j x, s i xâŸ« = 0 :=
  InnerProductSpace.gramSchmidt_inv_triangular _ _ hij

open Set

theorem mem_span_gramSchmidt (s : Î¹ â†’ (x : B) â†’ E x) {i j : Î¹} (hij : i â‰¤ j) (x) :
    s i x âˆˆ span â„ ((gramSchmidt s Â· x) '' Set.Iic j) :=
  InnerProductSpace.mem_span_gramSchmidt _ _ hij

theorem gramSchmidt_mem_span (s : Î¹ â†’ (x : B) â†’ E x) (x) :
    âˆ€ {j i}, i â‰¤ j â†’ gramSchmidt s i x âˆˆ span â„ ((s Â· x) '' Set.Iic j) :=
  InnerProductSpace.gramSchmidt_mem_span _ _

theorem span_gramSchmidt_Iic (s : Î¹ â†’ (x : B) â†’ E x) (c : Î¹) (x) :
    span â„ ((gramSchmidt s Â· x) '' Set.Iic c) = span â„ ((s Â· x) '' Set.Iic c) :=
  InnerProductSpace.span_gramSchmidt_Iic ..

theorem span_gramSchmidt_Iio (s : Î¹ â†’ (x : B) â†’ E x) (c : Î¹) (x) :
    span â„ ((gramSchmidt s Â· x) '' Set.Iio c) = span â„ ((s Â· x) '' Set.Iio c) :=
  InnerProductSpace.span_gramSchmidt_Iio _ _ _

/-- `gramSchmidt` preserves the point-wise span of sections. -/
theorem span_gramSchmidt (s : Î¹ â†’ (x : B) â†’ E x) (x) :
    span â„ (range (gramSchmidt s Â· x)) = span â„ (range (s Â· x)) :=
  InnerProductSpace.span_gramSchmidt â„ (s Â· x)

variable {x : B}

/-- If the section values `s i x` are orthogonal, `gramSchmidt` yields the same values at `x`. -/
theorem gramSchmidt_of_orthogonal (hs : Pairwise fun i j â†¦ âŸªs i x, s j xâŸ« = 0) :
    âˆ€ iâ‚€, gramSchmidt s iâ‚€ x = s iâ‚€ x := by
  simp_rw [gramSchmidt]
  exact fun i â†¦ congrFun (InnerProductSpace.gramSchmidt_of_orthogonal â„ hs) i

theorem gramSchmidt_ne_zero_coe (n : Î¹)
    (hâ‚€ : LinearIndependent â„ ((s Â· x) âˆ˜ ((â†‘) : Set.Iic n â†’ Î¹))) : gramSchmidt s n x â‰  0 :=
  InnerProductSpace.gramSchmidt_ne_zero_coe _ hâ‚€

/-- If the input sections of `gramSchmidt` are point-wise linearly independent,
the resulting sections are non-zero. -/
theorem gramSchmidt_ne_zero (s : Î¹ â†’ (x : B) â†’ E x) (n : Î¹) (hâ‚€ : LinearIndependent â„ (s Â· x)) :
    gramSchmidt s n x â‰  0 :=
  InnerProductSpace.gramSchmidt_ne_zero _ hâ‚€

-- For technical reasons, no version of `gramSchmidt_triangular` is provided so far:
-- it would expect a `Basis` (of vectors in `E x`) as input, whereas we would want a hypothesis
-- "the section values `s i x` form a basis" instead.

/-- `gramSchmidt` produces point-wise linearly independent sections when given linearly
independent sections. -/
theorem gramSchmidt_linearIndependent (hâ‚€ : LinearIndependent â„ (s Â· x)) :
    LinearIndependent â„ (gramSchmidt s Â· x) :=
  InnerProductSpace.gramSchmidt_linearIndependent hâ‚€

/-- When the sections `s` form a basis at `x`, so do the sections `gramSchmidt s`. -/
noncomputable def gramSchmidtBasis (hs : LinearIndependent â„ (s Â· x))
    (hs' : âŠ¤ â‰¤ span â„ (Set.range (s Â· x))) :
    Basis Î¹ â„ (E x) :=
  Basis.mk (gramSchmidt_linearIndependent hs)
    ((span_gramSchmidt s x).trans (eq_top_iff'.mpr fun _ â†¦ hs' trivial)).ge

theorem coe_gramSchmidtBasis (hs : LinearIndependent â„ (s Â· x))
    (hs' : âŠ¤ â‰¤ span â„ (Set.range (s Â· x))) :
    (gramSchmidtBasis hs hs') = (gramSchmidt s Â· x) :=
  Basis.coe_mk _ _

noncomputable def gramSchmidtNormed
    (s : Î¹ â†’ (x : B) â†’ E x) (n : Î¹) : (x : B) â†’ E x := fun x â†¦
  InnerProductSpace.gramSchmidtNormed â„ (s Â· x) n

lemma gramSchmidtNormed_coe {n : Î¹} :
    gramSchmidtNormed s n x = â€–gramSchmidt s n xâ€–â»Â¹ â€¢ gramSchmidt s n x := by
  simp [gramSchmidtNormed, InnerProductSpace.gramSchmidtNormed]

theorem gramSchmidtNormed_unit_length_coe {n : Î¹}
    (hâ‚€ : LinearIndependent â„ ((s Â· x) âˆ˜ ((â†‘) : Set.Iic n â†’ Î¹))) :
    â€–gramSchmidtNormed s n xâ€– = 1 :=
  InnerProductSpace.gramSchmidtNormed_unit_length_coe hâ‚€

theorem gramSchmidtNormed_unit_length {n : Î¹} (hâ‚€ : LinearIndependent â„ (s Â· x)) :
    â€–gramSchmidtNormed s n xâ€– = 1 :=
  InnerProductSpace.gramSchmidtNormed_unit_length hâ‚€

theorem gramSchmidtNormed_unit_length' {n : Î¹} (hn : gramSchmidtNormed s n x â‰  0) :
    â€–gramSchmidtNormed s n xâ€– = 1 :=
  InnerProductSpace.gramSchmidtNormed_unit_length' hn

/-- **Gram-Schmidt Orthonormalization**: `gramSchmidtNormed` applied to a point-wise linearly
independent set of sections produces a point-wise orthornormal system of sections. -/
theorem gramSchmidtNormed_orthonormal (hâ‚€ : LinearIndependent â„ (s Â· x)) :
    Orthonormal â„ (gramSchmidtNormed s Â· x) :=
  InnerProductSpace.gramSchmidtNormed_orthonormal hâ‚€

/-- **Gram-Schmidt Orthonormalization**: `gramSchmidtNormed` produces a point-wise orthornormal
system of sections after removing the sections which become zero in the process. -/
theorem gramSchmidtNormed_orthonormal' (s : Î¹ â†’ (x : B) â†’ E x) (x) :
    Orthonormal â„ fun i : { i | gramSchmidtNormed s i x â‰  0 } => gramSchmidtNormed s i x :=
  InnerProductSpace.gramSchmidtNormed_orthonormal' _

theorem span_gramSchmidtNormed (s : Î¹ â†’ (x : B) â†’ E x) (t : Set Î¹) :
    span â„ ((gramSchmidtNormed s Â· x) '' t) = span â„ ((gramSchmidt s Â· x) '' t) :=
  InnerProductSpace.span_gramSchmidtNormed (s Â· x) t

theorem span_gramSchmidtNormed_range (s : Î¹ â†’ (x : B) â†’ E x) :
    span â„ (range (gramSchmidtNormed s Â· x)) = span â„ (range (gramSchmidt s Â· x)) := by
  simpa only [image_univ.symm] using span_gramSchmidtNormed ..

/-- `gramSchmidtNormed` applied to linearly independent sections at a point `x` produces
sections which are linearly independent at `x`. -/
theorem gramSchmidtNormed_linearIndependent (hâ‚€ : LinearIndependent â„ (s Â· x)) :
    LinearIndependent â„ (gramSchmidtNormed s Â· x) := by
  simp [gramSchmidtNormed, InnerProductSpace.gramSchmidtNormed_linearIndependent hâ‚€]

lemma gramSchmidtNormed_apply_of_orthogonal (hs : Pairwise (âŸªs Â· x, s Â· xâŸ« = 0)) {i : Î¹} :
    gramSchmidtNormed s i x = â€–s i xâ€–â»Â¹ â€¢ s i x := by
  simp_rw [gramSchmidtNormed_coe, gramSchmidt_of_orthogonal hs i]

/-- If the section values `s i x` are orthonormal, applying `gramSchmidtNormed` yields the same
values at `x`. -/
lemma gramSchmidtNormed_apply_of_orthonormal (hs : Orthonormal â„ (s Â· x)) {i : Î¹} :
    gramSchmidtNormed s i x = s i x := by
  simp [gramSchmidtNormed_apply_of_orthogonal hs.2, hs.1 i]

/-- When the sections `s` form a basis at `x`, so do the sections `gramSchmidtNormed s`.

Note that `gramSchmidtOrthonormalBasis` proves a strictly stronger statement. -/
noncomputable def gramSchmidtNormedBasis (hs : LinearIndependent â„ (s Â· x))
    (hs' : âŠ¤ â‰¤ span â„ (Set.range (s Â· x))) :
    Basis Î¹ â„ (E x) :=
  Basis.mk (v := fun i â†¦ gramSchmidtNormed s i x) (gramSchmidtNormed_linearIndependent hs)
    (by rw [span_gramSchmidtNormed_range s, span_gramSchmidt s x]; exact hs')

@[simp]
theorem coe_gramSchmidtNormedBasis (hs : LinearIndependent â„ (s Â· x))
    (hs' : âŠ¤ â‰¤ span â„ (Set.range (s Â· x))) :
    (gramSchmidtNormedBasis hs hs' : Î¹ â†’ E x) = (gramSchmidtNormed s Â· x) :=
  Basis.coe_mk _ _

/-- If the sections `s` form a basis at `x`, the resulting sections form an orthonormal basis
at `x`.

We intentionally choose a different design from `InnerProductSpace.gramSchmidtOrthonormalBasis`,
as this is more convenient to work with in the application to local frames:
in this case, we know the sections form a basis, so linear independence and generating conditions
are easy to apply. Having to prove something about the cardinality of the indexing set would
be more tedious.
As we always obtain a basis, we need not consider the case of some resulting vector being zero.
-/
noncomputable def gramSchmidtOrthonormalBasis [Fintype Î¹]
    (hs : LinearIndependent â„ (s Â· x)) (hs' : âŠ¤ â‰¤ span â„ (Set.range (s Â· x))) :
    OrthonormalBasis Î¹ â„ (E x) := by
  apply (gramSchmidtNormedBasis hs hs').toOrthonormalBasis
  simp [gramSchmidtNormed_orthonormal hs]

@[simp]
theorem gramSchmidtOrthonormalBasis_coe [Fintype Î¹] (hs : LinearIndependent â„ (s Â· x))
    (hs' : âŠ¤ â‰¤ span â„ (Set.range (s Â· x))) :
    gramSchmidtOrthonormalBasis hs hs' = (gramSchmidtNormed s Â· x) := by
  simp [gramSchmidtOrthonormalBasis]

theorem gramSchmidtOrthonormalBasis_apply_of_orthonormal [Fintype Î¹]
    (hs : Orthonormal â„ (s Â· x)) (hs' : âŠ¤ â‰¤ span â„ (Set.range (s Â· x))) :
    (gramSchmidtOrthonormalBasis hs.linearIndependent hs') = (s Â· x) := by
  simp [gramSchmidtNormed_apply_of_orthonormal hs]

end VectorBundle

/-! The Gram-Schmidt process preserves continuity of sections -/
section continuity

section helper

variable {s t : (x : B) â†’ E x} {u : Set B} {x : B}

lemma continuousWithinAt_inner_div_norm_sq
    (hs : ContinuousWithinAt (T% s) u x) (ht : ContinuousWithinAt (T% t) u x) (hs' : s x â‰  0) :
    ContinuousWithinAt (fun x â†¦ âŸªs x, t xâŸ« / (â€–s xâ€– ^ 2)) u x := by
  have := (hs.inner_bundle ht).smul ((hs.inner_bundle hs).invâ‚€ (inner_self_ne_zero.mpr hs'))
  apply this.congr
  Â· intro y hy
    congr
    simp [inner_self_eq_norm_sq_to_K]
  Â· congr
    rw [â† real_inner_self_eq_norm_sq]

lemma continuousAt_inner_div_norm_sq
    (hs : ContinuousAt (T% s) x) (ht : ContinuousAt (T% t) x) (hs' : s x â‰  0) :
    ContinuousAt (fun x â†¦ âŸªs x, t xâŸ« / (â€–s xâ€– ^ 2)) x := by
  rw [â† continuousWithinAt_univ] at hs ht âŠ¢
  exact continuousWithinAt_inner_div_norm_sq hs ht hs'

def ContinuousWithinAt.starProjection
    (hs : ContinuousWithinAt (T% s) u x) (ht : ContinuousWithinAt (T% t) u x) (hs' : s x â‰  0) :
    ContinuousWithinAt (T% fun x â†¦ (Submodule.span â„ {s x}).starProjection (t x)) u x := by
  simp [Submodule.starProjection_singleton]
  exact (continuousWithinAt_inner_div_norm_sq hs ht hs').smul_section hs

lemma continuousWithinAt_inner (hs : ContinuousWithinAt (T% s) u x) :
    ContinuousWithinAt (â€–s Â·â€–) u x := by
  convert (Real.continuous_sqrt.continuousWithinAt).comp (hs.inner_bundle hs) (u.mapsTo_image _)
  simp [â† norm_eq_sqrt_real_inner]

end helper

variable {s : Î¹ â†’ (x : B) â†’ E x} {u : Set B} {x : B} {i : Î¹}

attribute [local instance] IsWellOrder.toHasWellFounded

lemma gramSchmidt_continuousWithinAt (hs : âˆ€ i, ContinuousWithinAt (T% (s i)) u x)
    {i : Î¹} (hs' : LinearIndependent â„ ((s Â· x) âˆ˜ ((â†‘) : Set.Iic i â†’ Î¹))) :
    ContinuousWithinAt (T% (VectorBundle.gramSchmidt s i)) u x := by
  suffices ContinuousWithinAt (T% (fun x â†¦ (s i x -
      âˆ‘ i_1 âˆˆ Iio i, (span â„ {VectorBundle.gramSchmidt s i_1 x}).starProjection (s i x)))) u x by
    apply ContinuousWithinAt.congr this
    Â· simp_rw [VectorBundle.gramSchmidt_def s i]; intros; trivial
    Â· rw [VectorBundle.gramSchmidt_def s i]
  refine ContinuousWithinAt.sub_section (ğ•œ := â„) (hs i) ?_
  refine ContinuousWithinAt.sum_section (ğ•œ := â„) ?_
  intro i' hi'
  let aux : { x // x âˆˆ Set.Iic i' } â†’ { x // x âˆˆ Set.Iic i } :=
    fun âŸ¨x, hxâŸ© â†¦ âŸ¨x, hx.trans (Finset.mem_Iio.mp hi').leâŸ©
  have : LinearIndependent â„ ((fun x_1 â†¦ s x_1 x) âˆ˜ @Subtype.val Î¹ fun x â†¦ x âˆˆ Set.Iic i') := by
    apply hs'.comp aux
    intro âŸ¨x, hxâŸ© âŸ¨x', hx'âŸ© h
    simp_all only [Subtype.mk.injEq, aux]
  exact ContinuousWithinAt.starProjection (gramSchmidt_continuousWithinAt hs this) (hs i)
    (VectorBundle.gramSchmidt_ne_zero_coe _ this)
termination_by i
decreasing_by exact (LocallyFiniteOrderBot.finset_mem_Iio i i').mp hi'

lemma gramSchmidt_continuousAt (hs : âˆ€ i, ContinuousAt (T% (s i)) x)
    (hs' : LinearIndependent â„ ((s Â· x) âˆ˜ ((â†‘) : Set.Iic i â†’ Î¹))) :
    ContinuousAt (T% (VectorBundle.gramSchmidt s i)) x := by
  simp_rw [â† continuousWithinAt_univ] at hs âŠ¢
  exact gramSchmidt_continuousWithinAt (fun i â†¦ hs i) hs'

lemma gramSchmidt_continuousOn (hs : âˆ€ i, ContinuousOn (T% (s i)) u)
    (hs' : âˆ€ x âˆˆ u, LinearIndependent â„ ((s Â· x) âˆ˜ ((â†‘) : Set.Iic i â†’ Î¹))) :
    ContinuousOn (T% (VectorBundle.gramSchmidt s i)) u :=
  fun x hx â†¦ gramSchmidt_continuousWithinAt (fun i â†¦ hs i x hx) (hs' _ hx)

lemma gramSchmidt_continuous (hs : âˆ€ i, Continuous (T% (s i)))
    (hs' : âˆ€ x, LinearIndependent â„ ((s Â· x) âˆ˜ ((â†‘) : Set.Iic i â†’ Î¹))) :
    Continuous (T% (VectorBundle.gramSchmidt s i)) := by
  simp_rw [continuous_iff_continuousAt] at hs âŠ¢
  exact fun x â†¦ gramSchmidt_continuousAt (fun i â†¦ hs i x) (hs' x)

lemma gramSchmidtNormed_continuousWithinAt (hs : âˆ€ i, ContinuousWithinAt (T% (s i)) u x)
    (hs' : LinearIndependent â„ ((s Â· x) âˆ˜ ((â†‘) : Set.Iic i â†’ Î¹))) :
    ContinuousWithinAt (T% (VectorBundle.gramSchmidtNormed s i)) u x := by
  have : ContinuousWithinAt (T%
      (fun x â†¦ â€–VectorBundle.gramSchmidt s i xâ€–â»Â¹ â€¢ VectorBundle.gramSchmidt s i x)) u x := by
    refine ContinuousWithinAt.smul_section (ğ•œ := â„) ?_
      (gramSchmidt_continuousWithinAt hs hs')
    refine (continuousWithinAt_inner (gramSchmidt_continuousWithinAt hs hs')).invâ‚€ ?_
    simpa using InnerProductSpace.gramSchmidt_ne_zero_coe i hs'
  exact this.congr (fun y hy â†¦ by congr) (by congr)

lemma gramSchmidtNormed_continuousAt (hs : âˆ€ i, ContinuousAt (T% (s i)) x)
    (hs' : LinearIndependent â„ ((s Â· x) âˆ˜ ((â†‘) : Set.Iic i â†’ Î¹))) :
    ContinuousAt (T% (VectorBundle.gramSchmidtNormed s i)) x := by
  simp_rw [â† continuousWithinAt_univ] at hs âŠ¢
  exact gramSchmidtNormed_continuousWithinAt (fun i â†¦ hs i) hs'

lemma gramSchmidtNormed_continuousOn (hs : âˆ€ i, ContinuousOn (T% (s i)) u)
    (hs' : âˆ€ x âˆˆ u, LinearIndependent â„ ((s Â· x) âˆ˜ ((â†‘) : Set.Iic i â†’ Î¹))) :
    ContinuousOn (T% (VectorBundle.gramSchmidtNormed s i)) u :=
  fun x hx â†¦ gramSchmidtNormed_continuousWithinAt (fun i â†¦ hs i x hx) (hs' _ hx)

lemma gramSchmidtNormed_continuous (hs : âˆ€ i, Continuous (T% (s i)))
    (hs' : âˆ€ x, LinearIndependent â„ ((s Â· x) âˆ˜ ((â†‘) : Set.Iic i â†’ Î¹))) :
    Continuous (T% (VectorBundle.gramSchmidtNormed s i)) := by
  simp_rw [continuous_iff_continuousAt] at hs âŠ¢
  exact fun x â†¦ gramSchmidtNormed_continuousAt (fun i â†¦ hs i x) (hs' x)

end continuity

/-! The Gram-Schmidt process preserves smoothness of sections -/
section smoothness

-- Let `V` be a smooth vector bundle with a `C^n` Riemannian structure over a `C^k` manifold `B`.
variable
  {EB : Type*} [NormedAddCommGroup EB] [NormedSpace â„ EB]
  {HB : Type*} [TopologicalSpace HB] {IB : ModelWithCorners â„ EB HB} {k n : WithTop â„•âˆ}
  {B : Type*} [TopologicalSpace B] [ChartedSpace HB B]
  {F : Type*} [NormedAddCommGroup F] [NormedSpace â„ F]
  {E : B â†’ Type*} [TopologicalSpace (TotalSpace F E)] [âˆ€ x, NormedAddCommGroup (E x)]
  [âˆ€ x, InnerProductSpace â„ (E x)] [FiberBundle F E] [VectorBundle â„ F E]
  [IsManifold IB k B]
  [IsContMDiffRiemannianBundle IB n F E]

-- TODO: fix pretty-printing of the new differential geometry elaborators!
set_option linter.style.commandStart false

section helper

variable {s t : (x : B) â†’ E x} {u : Set B} {x : B}

lemma contMDiffWithinAt_inner_div_norm_sq
    (hs : CMDiffAt[u] n (T% s) x) (ht : CMDiffAt[u] n (T% t) x) (hs' : s x â‰  0) :
    CMDiffAt[u] n (fun x â†¦ âŸªs x, t xâŸ« / (â€–s xâ€– ^ 2)) x := by
  have := (hs.inner_bundle ht).smul ((hs.inner_bundle hs).invâ‚€ (inner_self_ne_zero.mpr hs'))
  apply this.congr
  Â· intro y hy
    congr
    simp [inner_self_eq_norm_sq_to_K]
  Â· congr
    rw [â† real_inner_self_eq_norm_sq]

lemma contMDiffAt_inner_div_norm_sq
    (hs : CMDiffAt n (T% s) x) (ht : CMDiffAt n (T% t) x) (hs' : s x â‰  0) :
    CMDiffAt n (fun x â†¦ âŸªs x, t xâŸ« / (â€–s xâ€– ^ 2)) x := by
  rw [â† contMDiffWithinAt_univ] at hs ht âŠ¢
  exact contMDiffWithinAt_inner_div_norm_sq hs ht hs'

def ContMDiffWithinAt.starProjection
    (hs : CMDiffAt[u] n (T% s) x) (ht : CMDiffAt[u] n (T% t) x) (hs' : s x â‰  0) :
    CMDiffAt[u] n (T% fun x â†¦ (Submodule.span â„ {s x}).starProjection (t x)) x := by
  simp_rw [Submodule.starProjection_singleton]
  exact (contMDiffWithinAt_inner_div_norm_sq hs ht hs').smul_section hs

lemma contMDiffWithinAt_inner (hs : CMDiffAt[u] n (T% s) x) (hs' : s x â‰  0) :
    CMDiffAt[u] n (â€–s Â·â€–) x := by
  let F (x) := âŸªs x, s xâŸ«
  have aux : CMDiffAt[u] n (Real.sqrt âˆ˜ F) x := by
    have h1 : CMDiffAt[(F '' u)] n (Real.sqrt) (F x) := by
      apply ContMDiffAt.contMDiffWithinAt
      rw [contMDiffAt_iff_contDiffAt]
      exact Real.contDiffAt_sqrt (by simp [F, hs'])
    exact h1.comp x (hs.inner_bundle hs) (Set.mapsTo_image _ u)
  convert aux
  simp [F, â† norm_eq_sqrt_real_inner]

end helper

variable {s : Î¹ â†’ (x : B) â†’ E x} {u : Set B} {x : B} {i : Î¹}

attribute [local instance] IsWellOrder.toHasWellFounded

lemma gramSchmidt_contMDiffWithinAt (hs : âˆ€ i, CMDiffAt[u] n (T% (s i)) x)
    {i : Î¹} (hs' : LinearIndependent â„ ((s Â· x) âˆ˜ ((â†‘) : Set.Iic i â†’ Î¹))) :
    CMDiffAt[u] n (T% (VectorBundle.gramSchmidt s i)) x := by
  suffices CMDiffAt[u] n (T% (fun x â†¦ (s i x -
      âˆ‘ i_1 âˆˆ Iio i, (span â„ {VectorBundle.gramSchmidt s i_1 x}).starProjection (s i x)))) x by
    apply ContMDiffWithinAt.congr this
    Â· simp_rw [VectorBundle.gramSchmidt_def s i]; intros; trivial
    Â· rw [VectorBundle.gramSchmidt_def s i]
  apply (hs i).sub_section
  apply ContMDiffWithinAt.sum_section
  intro i' hi'
  let aux : { x // x âˆˆ Set.Iic i' } â†’ { x // x âˆˆ Set.Iic i } :=
    fun âŸ¨x, hxâŸ© â†¦ âŸ¨x, hx.trans (Finset.mem_Iio.mp hi').leâŸ©
  have : LinearIndependent â„ ((fun x_1 â†¦ s x_1 x) âˆ˜ @Subtype.val Î¹ fun x â†¦ x âˆˆ Set.Iic i') := by
    apply hs'.comp aux
    intro âŸ¨x, hxâŸ© âŸ¨x', hx'âŸ© h
    simp_all only [Subtype.mk.injEq, aux]
  apply ContMDiffWithinAt.starProjection (gramSchmidt_contMDiffWithinAt hs this) (hs i)
  apply VectorBundle.gramSchmidt_ne_zero_coe _ this
termination_by i
decreasing_by exact (LocallyFiniteOrderBot.finset_mem_Iio i i').mp hi'

lemma gramSchmidt_contMDiffAt (hs : âˆ€ i, CMDiffAt n (T% (s i)) x)
    (hs' : LinearIndependent â„ ((s Â· x) âˆ˜ ((â†‘) : Set.Iic i â†’ Î¹))) :
    CMDiffAt n (T% (VectorBundle.gramSchmidt s i)) x :=
  contMDiffWithinAt_univ.mpr <| gramSchmidt_contMDiffWithinAt (fun i â†¦ hs i) hs'

lemma gramSchmidt_contMDiffOn (hs : âˆ€ i, CMDiff[u] n (T% (s i)))
    (hs' : âˆ€ x âˆˆ u, LinearIndependent â„ ((s Â· x) âˆ˜ ((â†‘) : Set.Iic i â†’ Î¹))) :
    CMDiff[u] n (T% (VectorBundle.gramSchmidt s i)) :=
  fun x hx â†¦ gramSchmidt_contMDiffWithinAt (fun i â†¦ hs i x hx) (hs' _ hx)

lemma gramSchmidt_contMDiff (hs : âˆ€ i, CMDiff n (T% (s i)))
    (hs' : âˆ€ x, LinearIndependent â„ ((s Â· x) âˆ˜ ((â†‘) : Set.Iic i â†’ Î¹))) :
    CMDiff n (T% (VectorBundle.gramSchmidt s i)) :=
  fun x â†¦ gramSchmidt_contMDiffAt (fun i â†¦ hs i x) (hs' x)

lemma gramSchmidtNormed_contMDiffWithinAt (hs : âˆ€ i, CMDiffAt[u] n (T% (s i)) x)
    (hs' : LinearIndependent â„ ((s Â· x) âˆ˜ ((â†‘) : Set.Iic i â†’ Î¹))) :
    CMDiffAt[u] n (T% (VectorBundle.gramSchmidtNormed s i)) x := by
  have : CMDiffAt[u] n (T%
      (fun x â†¦ â€–VectorBundle.gramSchmidt s i xâ€–â»Â¹ â€¢ VectorBundle.gramSchmidt s i x)) x := by
    refine ContMDiffWithinAt.smul_section ?_ (gramSchmidt_contMDiffWithinAt hs hs')
    refine ContMDiffWithinAt.invâ‚€ ?_ ?_
    Â· refine contMDiffWithinAt_inner (gramSchmidt_contMDiffWithinAt hs hs') ?_
      simpa using InnerProductSpace.gramSchmidt_ne_zero_coe i hs'
    Â· simpa using InnerProductSpace.gramSchmidt_ne_zero_coe i hs'
  exact this.congr (fun y hy â†¦ by congr) (by congr)

lemma gramSchmidtNormed_contMDiffAt (hs : âˆ€ i, CMDiffAt n (T% (s i)) x)
    (hs' : LinearIndependent â„ ((s Â· x) âˆ˜ ((â†‘) : Set.Iic i â†’ Î¹)))
    : CMDiffAt n (T% (VectorBundle.gramSchmidtNormed s i)) x :=
  contMDiffWithinAt_univ.mpr <| gramSchmidtNormed_contMDiffWithinAt (fun i â†¦ hs i) hs'

lemma gramSchmidtNormed_contMDiffOn (hs : âˆ€ i, CMDiff[u] n (T% (s i)))
    (hs' : âˆ€ x âˆˆ u, LinearIndependent â„ ((s Â· x) âˆ˜ ((â†‘) : Set.Iic i â†’ Î¹))) :
    CMDiff[u] n (T% (VectorBundle.gramSchmidtNormed s i)) :=
  fun x hx â†¦ gramSchmidtNormed_contMDiffWithinAt (fun i â†¦ hs i x hx) (hs' _ hx)

lemma gramSchmidtNormed_contMDiff (hs : âˆ€ i, CMDiff n (T% (s i)))
    (hs' : âˆ€ x, LinearIndependent â„ ((s Â· x) âˆ˜ ((â†‘) : Set.Iic i â†’ Î¹))) :
    CMDiff n (T% (VectorBundle.gramSchmidtNormed s i)) :=
  fun x â†¦ gramSchmidtNormed_contMDiffAt (fun i â†¦ hs i x) (hs' x)

end smoothness
