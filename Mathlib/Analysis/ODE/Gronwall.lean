/-
Copyright (c) 2020 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov
-/
module

public import Mathlib.Analysis.SpecialFunctions.ExpDeriv

/-!
# Gr√∂nwall's inequality

The main technical result of this file is the Gr√∂nwall-like inequality
`norm_le_gronwallBound_of_norm_deriv_right_le`. It states that if `f : ‚Ñù ‚Üí E` satisfies `‚Äñf a‚Äñ ‚â§ Œ¥`
and `‚àÄ x ‚àà [a, b), ‚Äñf' x‚Äñ ‚â§ K * ‚Äñf x‚Äñ + Œµ`, then for all `x ‚àà [a, b]` we have `‚Äñf x‚Äñ ‚â§ Œ¥ * exp (K *
x) + (Œµ / K) * (exp (K * x) - 1)`.

Then we use this inequality to prove some estimates on the possible rate of growth of the distance
between two approximate or exact solutions of an ordinary differential equation.

The proofs are based on [Hubbard and West, *Differential Equations: A Dynamical Systems Approach*,
Sec. 4.5][HubbardWest-ode], where `norm_le_gronwallBound_of_norm_deriv_right_le` is called
‚ÄúFundamental Inequality‚Äù.

## TODO

- Once we have FTC, prove an inequality for a function satisfying `‚Äñf' x‚Äñ ‚â§ K x * ‚Äñf x‚Äñ + Œµ`,
  or more generally `liminf_{y‚Üíx+0} (f y - f x)/(y - x) ‚â§ K x * f x + Œµ` with any sign
  of `K x` and `f x`.
-/

@[expose] public section

open Metric Set Asymptotics Filter Real
open scoped Topology NNReal

variable {E : Type*} [NormedAddCommGroup E] [NormedSpace ‚Ñù E]

/-! ### Technical lemmas about `gronwallBound` -/


/-- Upper bound used in several Gr√∂nwall-like inequalities. -/
noncomputable def gronwallBound (Œ¥ K Œµ x : ‚Ñù) : ‚Ñù :=
  if K = 0 then Œ¥ + Œµ * x else Œ¥ * exp (K * x) + Œµ / K * (exp (K * x) - 1)

theorem gronwallBound_K0 (Œ¥ Œµ : ‚Ñù) : gronwallBound Œ¥ 0 Œµ = fun x => Œ¥ + Œµ * x :=
  funext fun _ => if_pos rfl

theorem gronwallBound_of_K_ne_0 {Œ¥ K Œµ : ‚Ñù} (hK : K ‚â† 0) :
    gronwallBound Œ¥ K Œµ = fun x => Œ¥ * exp (K * x) + Œµ / K * (exp (K * x) - 1) :=
  funext fun _ => if_neg hK

theorem hasDerivAt_gronwallBound (Œ¥ K Œµ x : ‚Ñù) :
    HasDerivAt (gronwallBound Œ¥ K Œµ) (K * gronwallBound Œ¥ K Œµ x + Œµ) x := by
  by_cases hK : K = 0
  ¬∑ subst K
    simp only [gronwallBound_K0, zero_mul, zero_add]
    convert ((hasDerivAt_id x).const_mul Œµ).const_add Œ¥
    rw [mul_one]
  ¬∑ simp only [gronwallBound_of_K_ne_0 hK]
    convert (((hasDerivAt_id x).const_mul K).exp.const_mul Œ¥).add
      ((((hasDerivAt_id x).const_mul K).exp.sub_const 1).const_mul (Œµ / K)) using 1
    simp only [id, mul_add, (mul_assoc _ _ _).symm, mul_comm _ K, mul_div_cancel‚ÇÄ _ hK]
    ring

theorem hasDerivAt_gronwallBound_shift (Œ¥ K Œµ x a : ‚Ñù) :
    HasDerivAt (fun y => gronwallBound Œ¥ K Œµ (y - a)) (K * gronwallBound Œ¥ K Œµ (x - a) + Œµ) x := by
  convert (hasDerivAt_gronwallBound Œ¥ K Œµ _).comp x ((hasDerivAt_id x).sub_const a) using 1
  rw [id, mul_one]

theorem gronwallBound_x0 (Œ¥ K Œµ : ‚Ñù) : gronwallBound Œ¥ K Œµ 0 = Œ¥ := by
  by_cases hK : K = 0
  ¬∑ simp only [gronwallBound, if_pos hK, mul_zero, add_zero]
  ¬∑ simp only [gronwallBound, if_neg hK, mul_zero, exp_zero, sub_self, mul_one,
      add_zero]

theorem gronwallBound_Œµ0 (Œ¥ K x : ‚Ñù) : gronwallBound Œ¥ K 0 x = Œ¥ * exp (K * x) := by
  by_cases hK : K = 0
  ¬∑ simp only [gronwallBound_K0, hK, zero_mul, exp_zero, add_zero, mul_one]
  ¬∑ simp only [gronwallBound_of_K_ne_0 hK, zero_div, zero_mul, add_zero]

theorem gronwallBound_Œµ0_Œ¥0 (K x : ‚Ñù) : gronwallBound 0 K 0 x = 0 := by
  simp only [gronwallBound_Œµ0, zero_mul]

theorem gronwallBound_continuous_Œµ (Œ¥ K x : ‚Ñù) : Continuous fun Œµ => gronwallBound Œ¥ K Œµ x := by
  by_cases hK : K = 0
  ¬∑ simp only [gronwallBound_K0, hK]
    fun_prop
  ¬∑ simp only [gronwallBound_of_K_ne_0 hK]
    fun_prop

/-- The Gronwall bound is monotone with respect to the time variable `x`. -/
lemma gronwallBound_mono {Œ¥ K Œµ : ‚Ñù} (hŒ¥ : 0 ‚â§ Œ¥) (hŒµ : 0 ‚â§ Œµ) (hK : 0 ‚â§ K) :
    Monotone (gronwallBound Œ¥ K Œµ) := by
  intro x‚ÇÅ x‚ÇÇ hx
  unfold gronwallBound
  split_ifs with hK‚ÇÄ
  ¬∑ gcongr
  ¬∑ have hK_pos : 0 < K := by positivity
    gcongr

/-! ### Inequality and corollaries -/

/-- A Gr√∂nwall-like inequality: if `f : ‚Ñù ‚Üí ‚Ñù` is continuous on `[a, b]` and satisfies
the inequalities `f a ‚â§ Œ¥` and
`‚àÄ x ‚àà [a, b), liminf_{z‚Üíx+0} (f z - f x)/(z - x) ‚â§ K * (f x) + Œµ`, then `f x`
is bounded by `gronwallBound Œ¥ K Œµ (x - a)` on `[a, b]`.

See also `norm_le_gronwallBound_of_norm_deriv_right_le` for a version bounding `‚Äñf x‚Äñ`,
`f : ‚Ñù ‚Üí E`. -/
theorem le_gronwallBound_of_liminf_deriv_right_le {f f' : ‚Ñù ‚Üí ‚Ñù} {Œ¥ K Œµ : ‚Ñù} {a b : ‚Ñù}
    (hf : ContinuousOn f (Icc a b))
    (hf' : ‚àÄ x ‚àà Ico a b, ‚àÄ r, f' x < r ‚Üí ‚àÉ·∂† z in ùìù[>] x, (z - x)‚Åª¬π * (f z - f x) < r)
    (ha : f a ‚â§ Œ¥) (bound : ‚àÄ x ‚àà Ico a b, f' x ‚â§ K * f x + Œµ) :
    ‚àÄ x ‚àà Icc a b, f x ‚â§ gronwallBound Œ¥ K Œµ (x - a) := by
  have H : ‚àÄ x ‚àà Icc a b, ‚àÄ Œµ' ‚àà Ioi Œµ, f x ‚â§ gronwallBound Œ¥ K Œµ' (x - a) := by
    intro x hx Œµ' (hŒµ' : Œµ < Œµ')
    apply image_le_of_liminf_slope_right_lt_deriv_boundary hf hf'
    ¬∑ rwa [sub_self, gronwallBound_x0]
    ¬∑ exact fun x => hasDerivAt_gronwallBound_shift Œ¥ K Œµ' x a
    ¬∑ intro x hx hfB
      grw [‚Üê hfB, bound x hx]
      gcongr
    ¬∑ exact hx
  intro x hx
  change f x ‚â§ (fun Œµ' => gronwallBound Œ¥ K Œµ' (x - a)) Œµ
  convert continuousWithinAt_const.closure_le _ _ (H x hx)
  ¬∑ simp only [closure_Ioi, self_mem_Ici]
  exact (gronwallBound_continuous_Œµ Œ¥ K (x - a)).continuousWithinAt

/-- A Gr√∂nwall-like inequality: if `f : ‚Ñù ‚Üí E` is continuous on `[a, b]`, has right derivative
`f' x` at every point `x ‚àà [a, b)`, and satisfies the inequalities `‚Äñf a‚Äñ ‚â§ Œ¥`,
`‚àÄ x ‚àà [a, b), ‚Äñf' x‚Äñ ‚â§ K * ‚Äñf x‚Äñ + Œµ`, then `‚Äñf x‚Äñ` is bounded by `gronwallBound Œ¥ K Œµ (x - a)`
on `[a, b]`. -/
theorem norm_le_gronwallBound_of_norm_deriv_right_le {f f' : ‚Ñù ‚Üí E} {Œ¥ K Œµ : ‚Ñù} {a b : ‚Ñù}
    (hf : ContinuousOn f (Icc a b)) (hf' : ‚àÄ x ‚àà Ico a b, HasDerivWithinAt f (f' x) (Ici x) x)
    (ha : ‚Äñf a‚Äñ ‚â§ Œ¥) (bound : ‚àÄ x ‚àà Ico a b, ‚Äñf' x‚Äñ ‚â§ K * ‚Äñf x‚Äñ + Œµ) :
    ‚àÄ x ‚àà Icc a b, ‚Äñf x‚Äñ ‚â§ gronwallBound Œ¥ K Œµ (x - a) :=
  le_gronwallBound_of_liminf_deriv_right_le (continuous_norm.comp_continuousOn hf)
    (fun x hx _r hr => (hf' x hx).liminf_right_slope_norm_le hr) ha bound

/-- Let `f : [a, b] ‚Üí E` be a differentiable function such that `f a = 0`
and `‚Äñf'(x)‚Äñ ‚â§ K ‚Äñf(x)‚Äñ` for some constant `K`. Then `f = 0` on `[a, b]`. -/
theorem eq_zero_of_abs_deriv_le_mul_abs_self_of_eq_zero_right {f f' : ‚Ñù ‚Üí E} {K a b : ‚Ñù}
    (hf : ContinuousOn f (Icc a b)) (hf' : ‚àÄ x ‚àà Ico a b, HasDerivWithinAt f (f' x) (Ici x) x)
    (ha : f a = 0) (bound : ‚àÄ x ‚àà Ico a b, ‚Äñf' x‚Äñ ‚â§ K * ‚Äñf x‚Äñ) :
    ‚àÄ x ‚àà Set.Icc a b, f x = 0 := by
  intro x hx
  apply norm_le_zero_iff.mp
  calc ‚Äñf x‚Äñ
    _ ‚â§ gronwallBound 0 K 0 (x - a) :=
      norm_le_gronwallBound_of_norm_deriv_right_le hf hf' (by simp [ha]) (by simpa using bound) _ hx
    _ = 0 := by rw [gronwallBound_Œµ0_Œ¥0]

variable {v : ‚Ñù ‚Üí E ‚Üí E} {s : ‚Ñù ‚Üí Set E} {K : ‚Ñù‚â•0} {f g f' g' : ‚Ñù ‚Üí E} {a b t‚ÇÄ : ‚Ñù} {Œµf Œµg Œ¥ : ‚Ñù}

/-- If `f` and `g` are two approximate solutions of the same ODE, then the distance between them
can't grow faster than exponentially. This is a simple corollary of Gr√∂nwall's inequality, and some
people call this Gr√∂nwall's inequality too.

This version assumes all inequalities to be true in some time-dependent set `s t`,
and assumes that the solutions never leave this set. -/
theorem dist_le_of_approx_trajectories_ODE_of_mem
    (hv : ‚àÄ t ‚àà Ico a b, LipschitzOnWith K (v t) (s t))
    (hf : ContinuousOn f (Icc a b))
    (hf' : ‚àÄ t ‚àà Ico a b, HasDerivWithinAt f (f' t) (Ici t) t)
    (f_bound : ‚àÄ t ‚àà Ico a b, dist (f' t) (v t (f t)) ‚â§ Œµf)
    (hfs : ‚àÄ t ‚àà Ico a b, f t ‚àà s t)
    (hg : ContinuousOn g (Icc a b))
    (hg' : ‚àÄ t ‚àà Ico a b, HasDerivWithinAt g (g' t) (Ici t) t)
    (g_bound : ‚àÄ t ‚àà Ico a b, dist (g' t) (v t (g t)) ‚â§ Œµg)
    (hgs : ‚àÄ t ‚àà Ico a b, g t ‚àà s t)
    (ha : dist (f a) (g a) ‚â§ Œ¥) :
    ‚àÄ t ‚àà Icc a b, dist (f t) (g t) ‚â§ gronwallBound Œ¥ K (Œµf + Œµg) (t - a) := by
  simp only [dist_eq_norm] at ha ‚ä¢
  have h_deriv : ‚àÄ t ‚àà Ico a b, HasDerivWithinAt (fun t => f t - g t) (f' t - g' t) (Ici t) t :=
    fun t ht => (hf' t ht).sub (hg' t ht)
  apply norm_le_gronwallBound_of_norm_deriv_right_le (hf.sub hg) h_deriv ha
  intro t ht
  have := dist_triangle4_right (f' t) (g' t) (v t (f t)) (v t (g t))
  have hv := (hv t ht).dist_le_mul _ (hfs t ht) _ (hgs t ht)
  rw [‚Üê dist_eq_norm, ‚Üê dist_eq_norm]
  refine this.trans ((add_le_add (add_le_add (f_bound t ht) (g_bound t ht)) hv).trans ?_)
  rw [add_comm]

/-- If `f` and `g` are two approximate solutions of the same ODE, then the distance between them
can't grow faster than exponentially. This is a simple corollary of Gr√∂nwall's inequality, and some
people call this Gr√∂nwall's inequality too.

This version assumes all inequalities to be true in the whole space. -/
theorem dist_le_of_approx_trajectories_ODE
    (hv : ‚àÄ t, LipschitzWith K (v t))
    (hf : ContinuousOn f (Icc a b))
    (hf' : ‚àÄ t ‚àà Ico a b, HasDerivWithinAt f (f' t) (Ici t) t)
    (f_bound : ‚àÄ t ‚àà Ico a b, dist (f' t) (v t (f t)) ‚â§ Œµf)
    (hg : ContinuousOn g (Icc a b))
    (hg' : ‚àÄ t ‚àà Ico a b, HasDerivWithinAt g (g' t) (Ici t) t)
    (g_bound : ‚àÄ t ‚àà Ico a b, dist (g' t) (v t (g t)) ‚â§ Œµg)
    (ha : dist (f a) (g a) ‚â§ Œ¥) :
    ‚àÄ t ‚àà Icc a b, dist (f t) (g t) ‚â§ gronwallBound Œ¥ K (Œµf + Œµg) (t - a) :=
  have hfs : ‚àÄ t ‚àà Ico a b, f t ‚àà @univ E := fun _ _ => trivial
  dist_le_of_approx_trajectories_ODE_of_mem (fun t _ => (hv t).lipschitzOnWith) hf hf'
    f_bound hfs hg hg' g_bound (fun _ _ => trivial) ha

/-- If `f` and `g` are two exact solutions of the same ODE, then the distance between them
can't grow faster than exponentially. This is a simple corollary of Gr√∂nwall's inequality, and some
people call this Gr√∂nwall's inequality too.

This version assumes all inequalities to be true in some time-dependent set `s t`,
and assumes that the solutions never leave this set. -/
theorem dist_le_of_trajectories_ODE_of_mem
    (hv : ‚àÄ t ‚àà Ico a b, LipschitzOnWith K (v t) (s t))
    (hf : ContinuousOn f (Icc a b))
    (hf' : ‚àÄ t ‚àà Ico a b, HasDerivWithinAt f (v t (f t)) (Ici t) t)
    (hfs : ‚àÄ t ‚àà Ico a b, f t ‚àà s t)
    (hg : ContinuousOn g (Icc a b)) (hg' : ‚àÄ t ‚àà Ico a b, HasDerivWithinAt g (v t (g t)) (Ici t) t)
    (hgs : ‚àÄ t ‚àà Ico a b, g t ‚àà s t) (ha : dist (f a) (g a) ‚â§ Œ¥) :
    ‚àÄ t ‚àà Icc a b, dist (f t) (g t) ‚â§ Œ¥ * exp (K * (t - a)) := by
  have f_bound : ‚àÄ t ‚àà Ico a b, dist (v t (f t)) (v t (f t)) ‚â§ 0 := by intros; rw [dist_self]
  have g_bound : ‚àÄ t ‚àà Ico a b, dist (v t (g t)) (v t (g t)) ‚â§ 0 := by intros; rw [dist_self]
  intro t ht
  have :=
    dist_le_of_approx_trajectories_ODE_of_mem hv hf hf' f_bound hfs hg hg' g_bound hgs ha t ht
  rwa [zero_add, gronwallBound_Œµ0] at this

/-- If `f` and `g` are two exact solutions of the same ODE, then the distance between them
can't grow faster than exponentially. This is a simple corollary of Gr√∂nwall's inequality, and some
people call this Gr√∂nwall's inequality too.

This version assumes all inequalities to be true in the whole space. -/
theorem dist_le_of_trajectories_ODE
    (hv : ‚àÄ t, LipschitzWith K (v t))
    (hf : ContinuousOn f (Icc a b))
    (hf' : ‚àÄ t ‚àà Ico a b, HasDerivWithinAt f (v t (f t)) (Ici t) t)
    (hg : ContinuousOn g (Icc a b))
    (hg' : ‚àÄ t ‚àà Ico a b, HasDerivWithinAt g (v t (g t)) (Ici t) t)
    (ha : dist (f a) (g a) ‚â§ Œ¥) :
    ‚àÄ t ‚àà Icc a b, dist (f t) (g t) ‚â§ Œ¥ * exp (K * (t - a)) :=
  have hfs : ‚àÄ t ‚àà Ico a b, f t ‚àà @univ E := fun _ _ => trivial
  dist_le_of_trajectories_ODE_of_mem (fun t _ => (hv t).lipschitzOnWith) hf hf' hfs hg
    hg' (fun _ _ => trivial) ha
