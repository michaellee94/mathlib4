/-
Copyright (c) 2026 Winston Yin. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Winston Yin
-/
import Mathlib.Analysis.ODE.Basic
import Mathlib.Analysis.ODE.Gronwall
import Mathlib.Analysis.ODE.PicardLindelof

/-!
# Existence and uniqueness of integral curves in normed spaces

This file states the results of Gronwall's inequality and the Picard-LindelÃ¶f theorem in terms
of the integral curve API (`IsIntegralCurve`, `IsIntegralCurveOn`, `IsIntegralCurveAt`).

## Main results

## Tags

integral curve, vector field, existence, uniqueness, Picard-LindelÃ¶f, Gronwall
-/

@[expose] public section

open Function intervalIntegral MeasureTheory Metric Set
open scoped Nat NNReal Topology

/-! ## Existence of solutions to ODEs -/

namespace IsPicardLindelof

open ODE

variable {E : Type*} [NormedAddCommGroup E] [NormedSpace â„ E] [CompleteSpace E]
  {f : â„ â†’ E â†’ E} {tmin tmax : â„} {tâ‚€ : Icc tmin tmax} {xâ‚€ x : E} {a r L K : â„â‰¥0}

/-- **Picard-LindelÃ¶f (Cauchy-Lipschitz) theorem**, differential form. This version shows the
existence of a local solution whose initial point `x` may be different from the centre `xâ‚€` of
the closed ball within which the properties of the vector field hold. -/
theorem exists_eq_forall_mem_Icc_hasDerivWithinAt
    (hf : IsPicardLindelof f tâ‚€ xâ‚€ a r L K) (hx : x âˆˆ closedBall xâ‚€ r) :
    âˆƒ Î± : â„ â†’ E, Î± tâ‚€ = x âˆ§
      âˆ€ t âˆˆ Icc tmin tmax, HasDerivWithinAt Î± (f t (Î± t)) (Icc tmin tmax) t := by
  obtain âŸ¨Î±, hÎ±âŸ© := FunSpace.exists_isFixedPt_next hf hx
  refine âŸ¨Î±.compProj, by rw [FunSpace.compProj_val, â† hÎ±, FunSpace.next_applyâ‚€], fun t ht â†¦ ?_âŸ©
  apply hasDerivWithinAt_picard_Icc tâ‚€.2 hf.continuousOn_uncurry
    Î±.continuous_compProj.continuousOn (fun _ ht' â†¦ Î±.compProj_mem_closedBall hf.mul_max_le)
    x ht |>.congr_of_mem _ ht
  intro t' ht'
  nth_rw 1 [â† hÎ±]
  rw [FunSpace.compProj_of_mem ht', FunSpace.next_apply]

/-- **Picard-LindelÃ¶f (Cauchy-Lipschitz) theorem**, differential form. -/
theorem exists_eq_forall_mem_Icc_hasDerivWithinAtâ‚€
    (hf : IsPicardLindelof f tâ‚€ xâ‚€ a 0 L K) :
    âˆƒ Î± : â„ â†’ E, Î± tâ‚€ = xâ‚€ âˆ§
      âˆ€ t âˆˆ Icc tmin tmax, HasDerivWithinAt Î± (f t (Î± t)) (Icc tmin tmax) t :=
  exists_eq_forall_mem_Icc_hasDerivWithinAt hf (mem_closedBall_self le_rfl)

end IsPicardLindelof

/-! ## $C^1$ vector field -/

namespace ContDiffAt

variable {E : Type*} [NormedAddCommGroup E] [NormedSpace â„ E] [CompleteSpace E]
  {f : E â†’ E} {xâ‚€ : E}

/-- If a vector field `f : E â†’ E` is continuously differentiable at `xâ‚€ : E`, then it admits an
integral curve `Î± : â„ â†’ E` defined on an open interval, with initial condition `Î± tâ‚€ = x`, where
`x` may be different from `xâ‚€`. -/
theorem exists_forall_mem_closedBall_exists_eq_forall_mem_Ioo_hasDerivAt
    (hf : ContDiffAt â„ 1 f xâ‚€) (tâ‚€ : â„) :
    âˆƒ r > (0 : â„), âˆƒ Îµ > (0 : â„), âˆ€ x âˆˆ closedBall xâ‚€ r, âˆƒ Î± : â„ â†’ E, Î± tâ‚€ = x âˆ§
      âˆ€ t âˆˆ Ioo (tâ‚€ - Îµ) (tâ‚€ + Îµ), HasDerivAt Î± (f (Î± t)) t := by
  have âŸ¨Îµ, hÎµ, a, r, _, _, hr, hplâŸ© := IsPicardLindelof.of_contDiffAt_one hf tâ‚€
  refine âŸ¨r, hr, Îµ, hÎµ, fun x hx â†¦ ?_âŸ©
  have âŸ¨Î±, hÎ±1, hÎ±2âŸ© := hpl.exists_eq_forall_mem_Icc_hasDerivWithinAt hx
  refine âŸ¨Î±, hÎ±1, fun t ht â†¦ ?_âŸ©
  exact hÎ±2 t (Ioo_subset_Icc_self ht) |>.hasDerivAt (Icc_mem_nhds ht.1 ht.2)

/-- If a vector field `f : E â†’ E` is continuously differentiable at `xâ‚€ : E`, then it admits an
integral curve `Î± : â„ â†’ E` defined on an open interval, with initial condition `Î± tâ‚€ = xâ‚€`. -/
theorem exists_forall_mem_closedBall_exists_eq_forall_mem_Ioo_hasDerivAtâ‚€
    (hf : ContDiffAt â„ 1 f xâ‚€) (tâ‚€ : â„) :
    âˆƒ Î± : â„ â†’ E, Î± tâ‚€ = xâ‚€ âˆ§ âˆƒ Îµ > (0 : â„),
      âˆ€ t âˆˆ Ioo (tâ‚€ - Îµ) (tâ‚€ + Îµ), HasDerivAt Î± (f (Î± t)) t :=
  have âŸ¨_, hr, Îµ, hÎµ, HâŸ© := exists_forall_mem_closedBall_exists_eq_forall_mem_Ioo_hasDerivAt hf tâ‚€
  have âŸ¨Î±, hÎ±1, hÎ±2âŸ© := H xâ‚€ (mem_closedBall_self (le_of_lt hr))
  âŸ¨Î±, hÎ±1, Îµ, hÎµ, hÎ±2âŸ©

open Classical in
/-- If a vector field `f : E â†’ E` is continuously differentiable at `xâ‚€ : E`, then it admits a flow
`Î± : E â†’ â„ â†’ E` defined on an open domain, with initial condition `Î± x tâ‚€ = x` for all `x` within
the domain. -/
theorem exists_eventually_eq_hasDerivAt
    (hf : ContDiffAt â„ 1 f xâ‚€) (tâ‚€ : â„) :
    âˆƒ Î± : E â†’ â„ â†’ E, âˆ€á¶  xt in ğ“ xâ‚€ Ã—Ë¢ ğ“ tâ‚€,
      Î± xt.1 tâ‚€ = xt.1 âˆ§ HasDerivAt (Î± xt.1) (f (Î± xt.1 xt.2)) xt.2 := by
  obtain âŸ¨r, hr, Îµ, hÎµ, HâŸ© := exists_forall_mem_closedBall_exists_eq_forall_mem_Ioo_hasDerivAt hf tâ‚€
  choose Î± hÎ± using H
  refine âŸ¨fun (x : E) â†¦ if hx : x âˆˆ closedBall xâ‚€ r then Î± x hx else 0, ?_âŸ©
  rw [Filter.eventually_iff_exists_mem]
  refine âŸ¨closedBall xâ‚€ r Ã—Ë¢ Ioo (tâ‚€ - Îµ) (tâ‚€ + Îµ), ?_, ?_âŸ©
  Â· rw [Filter.prod_mem_prod_iff]
    exact âŸ¨closedBall_mem_nhds xâ‚€ hr, Ioo_mem_nhds (by linarith) (by linarith)âŸ©
  Â· grind

end ContDiffAt
