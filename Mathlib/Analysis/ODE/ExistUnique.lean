/-
Copyright (c) 2026 Winston Yin. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Winston Yin
-/
module

public import Mathlib.Analysis.ODE.Basic
public import Mathlib.Analysis.ODE.Gronwall
public import Mathlib.Analysis.ODE.PicardLindelof

/-!
# Existence and uniqueness of integral curves in normed spaces

This file states the results of Gronwall's inequality and the Picard-Lindel√∂f theorem in terms
of the integral curve API (`IsIntegralCurve`, `IsIntegralCurveOn`, `IsIntegralCurveAt`).

## Main results

* `IsPicardLindelof.exists_eq_isIntegralCurveOn`: the Picard-Lindel√∂f theorem, stating the
  existence of a local integral curve to a time-dependent ODE.
* `IsPicardLindelof.exists_forall_mem_closedBall_eq_isIntegralCurveOn_lipschitzOnWith`: the
  existence of a local flow that is Lipschitz continuous in the initial point.
* `IsPicardLindelof.exists_forall_mem_closedBall_eq_isIntegralCurveOn_continuousOn`: the existence
  of a local flow that is continuous on its domain as a map `E √ó ‚Ñù ‚Üí E`.
* `IsPicardLindelof.exists_forall_mem_closedBall_eq_isIntegralCurveOn`: the existence of a local
  flow to a time-dependent vector field.
* `ContDiffAt.exists_forall_mem_closedBall_exists_eq_isIntegralCurveOn`: a `C¬π` vector field
  admits integral curves on open intervals for all nearby initial points.
* `ContDiffAt.exists_eq_isIntegralCurveAt`: a `C¬π` vector field admits a local integral curve.
* `ContDiffAt.exists_eventually_isIntegralCurveAt`: a `C¬π` vector field admits a local flow.

## Tags

integral curve, vector field, existence, uniqueness, Picard-Lindel√∂f, Gronwall
-/

@[expose] public section

open Function intervalIntegral MeasureTheory Metric Set
open scoped Nat NNReal Topology

/-! ## Existence of solutions to ODEs -/

namespace IsPicardLindelof

open ODE

variable {E : Type*} [NormedAddCommGroup E] [NormedSpace ‚Ñù E] [CompleteSpace E]
  {f : ‚Ñù ‚Üí E ‚Üí E} {tmin tmax : ‚Ñù} {t‚ÇÄ : Icc tmin tmax} {x‚ÇÄ x : E} {a r L K : ‚Ñù‚â•0}

/-- **Picard-Lindel√∂f (Cauchy-Lipschitz) theorem**, differential form. This version shows the
existence of a local solution whose initial point `x` may be different from the centre `x‚ÇÄ` of
the closed ball within which the properties of the vector field hold. -/
theorem exists_eq_isIntegralCurveOn
    (hf : IsPicardLindelof f t‚ÇÄ x‚ÇÄ a r L K) (hx : x ‚àà closedBall x‚ÇÄ r) :
    ‚àÉ Œ± : ‚Ñù ‚Üí E, Œ± t‚ÇÄ = x ‚àß IsIntegralCurveOn Œ± f (Icc tmin tmax) := by
  obtain ‚ü®Œ±, hŒ±‚ü© := FunSpace.exists_isFixedPt_next hf hx
  refine ‚ü®Œ±.compProj, by rw [FunSpace.compProj_val, ‚Üê hŒ±, FunSpace.next_apply‚ÇÄ], fun t ht ‚Ü¶ ?_‚ü©
  apply hasDerivWithinAt_picard_Icc t‚ÇÄ.2 hf.continuousOn_uncurry
    Œ±.continuous_compProj.continuousOn (fun _ ht' ‚Ü¶ Œ±.compProj_mem_closedBall hf.mul_max_le)
    x ht |>.congr_of_mem _ ht
  intro t' ht'
  nth_rw 1 [‚Üê hŒ±]
  rw [FunSpace.compProj_of_mem ht', FunSpace.next_apply]

/-- **Picard-Lindel√∂f (Cauchy-Lipschitz) theorem**, differential form. -/
theorem exists_eq_isIntegralCurveOn‚ÇÄ
    (hf : IsPicardLindelof f t‚ÇÄ x‚ÇÄ a 0 L K) :
    ‚àÉ Œ± : ‚Ñù ‚Üí E, Œ± t‚ÇÄ = x‚ÇÄ ‚àß IsIntegralCurveOn Œ± f (Icc tmin tmax) :=
  exists_eq_isIntegralCurveOn hf (mem_closedBall_self le_rfl)

open Classical in
/-- **Picard-Lindel√∂f (Cauchy-Lipschitz) theorem**, differential form. This version shows the
existence of a local flow and that it is Lipschitz continuous in the initial point. -/
theorem exists_forall_mem_closedBall_eq_isIntegralCurveOn_lipschitzOnWith
    (hf : IsPicardLindelof f t‚ÇÄ x‚ÇÄ a r L K) :
    ‚àÉ Œ± : E ‚Üí ‚Ñù ‚Üí E, (‚àÄ x ‚àà closedBall x‚ÇÄ r, Œ± x t‚ÇÄ = x ‚àß
      IsIntegralCurveOn (Œ± x) f (Icc tmin tmax)) ‚àß
      ‚àÉ L' : ‚Ñù‚â•0, ‚àÄ t ‚àà Icc tmin tmax, LipschitzOnWith L' (Œ± ¬∑ t) (closedBall x‚ÇÄ r) := by
  have (x) (hx : x ‚àà closedBall x‚ÇÄ r) := FunSpace.exists_isFixedPt_next hf hx
  choose Œ± hŒ± using this
  set Œ±' := fun (x : E) ‚Ü¶ if hx : x ‚àà closedBall x‚ÇÄ r then
    Œ± x hx |>.compProj else 0 with hŒ±'
  refine ‚ü®Œ±', fun x hx ‚Ü¶ ‚ü®?_, fun t ht ‚Ü¶ ?_‚ü©, ?_‚ü©
  ¬∑ rw [hŒ±']
    beta_reduce
    rw [dif_pos hx, FunSpace.compProj_val, ‚Üê hŒ±, FunSpace.next_apply‚ÇÄ]
  ¬∑ rw [hŒ±']
    beta_reduce
    rw [dif_pos hx, FunSpace.compProj_apply]
    apply hasDerivWithinAt_picard_Icc t‚ÇÄ.2 hf.continuousOn_uncurry
      (Œ± x hx |>.continuous_compProj.continuousOn)
      (fun _ ht' ‚Ü¶ Œ± x hx |>.compProj_mem_closedBall hf.mul_max_le)
      x ht |>.congr_of_mem _ ht
    intro t' ht'
    nth_rw 1 [‚Üê hŒ±]
    rw [FunSpace.compProj_of_mem ht', FunSpace.next_apply]
  ¬∑ obtain ‚ü®L', h‚ü© := FunSpace.exists_forall_closedBall_funSpace_dist_le_mul hf
    refine ‚ü®L', fun t ht ‚Ü¶ LipschitzOnWith.of_dist_le_mul fun x hx y hy ‚Ü¶ ?_‚ü©
    simp_rw [hŒ±']
    rw [dif_pos hx, dif_pos hy, FunSpace.compProj_apply, FunSpace.compProj_apply,
      ‚Üê FunSpace.toContinuousMap_apply_eq_apply, ‚Üê FunSpace.toContinuousMap_apply_eq_apply]
    have : Nonempty (Icc tmin tmax) := ‚ü®t‚ÇÄ‚ü©
    apply ContinuousMap.dist_le_iff_of_nonempty.mp
    exact h x y hx hy (Œ± x hx) (Œ± y hy) (hŒ± x hx) (hŒ± y hy)

/-- **Picard-Lindel√∂f (Cauchy-Lipschitz) theorem**, differential form. This version shows the
existence of a local flow and that it is continuous on its domain as a (partial) map `E √ó ‚Ñù ‚Üí E`. -/
theorem exists_forall_mem_closedBall_eq_isIntegralCurveOn_continuousOn
    (hf : IsPicardLindelof f t‚ÇÄ x‚ÇÄ a r L K) :
    ‚àÉ Œ± : E √ó ‚Ñù ‚Üí E, (‚àÄ x ‚àà closedBall x‚ÇÄ r, Œ± ‚ü®x, t‚ÇÄ‚ü© = x ‚àß
      IsIntegralCurveOn (Œ± ‚ü®x, ¬∑‚ü©) f (Icc tmin tmax)) ‚àß
      ContinuousOn Œ± (closedBall x‚ÇÄ r √óÀ¢ Icc tmin tmax) := by
  obtain ‚ü®Œ±, hŒ±1, L', hŒ±2‚ü© := hf.exists_forall_mem_closedBall_eq_isIntegralCurveOn_lipschitzOnWith
  refine ‚ü®uncurry Œ±, hŒ±1, ?_‚ü©
  apply continuousOn_prod_of_continuousOn_lipschitzOnWith _ L' _ hŒ±2
  exact fun x hx ‚Ü¶ (hŒ±1 x hx).2.continuousOn

/-- **Picard-Lindel√∂f (Cauchy-Lipschitz) theorem**, differential form. This version shows the
existence of a local flow. -/
theorem exists_forall_mem_closedBall_eq_isIntegralCurveOn
    (hf : IsPicardLindelof f t‚ÇÄ x‚ÇÄ a r L K) :
    ‚àÉ Œ± : E ‚Üí ‚Ñù ‚Üí E, ‚àÄ x ‚àà closedBall x‚ÇÄ r, Œ± x t‚ÇÄ = x ‚àß
      IsIntegralCurveOn (Œ± x) f (Icc tmin tmax) :=
  have ‚ü®Œ±, hŒ±‚ü© := exists_forall_mem_closedBall_eq_isIntegralCurveOn_lipschitzOnWith hf
  ‚ü®Œ±, hŒ±.1‚ü©

end IsPicardLindelof

/-! ## $C^1$ vector field -/

namespace ContDiffAt

variable {E : Type*} [NormedAddCommGroup E] [NormedSpace ‚Ñù E] [CompleteSpace E]
  {f : E ‚Üí E} {x‚ÇÄ : E}

/-- If a vector field `f : E ‚Üí E` is continuously differentiable at `x‚ÇÄ : E`, then it admits an
integral curve `Œ± : ‚Ñù ‚Üí E` defined on an open interval, with initial condition `Œ± t‚ÇÄ = x`, where
`x` may be different from `x‚ÇÄ`. -/
theorem exists_forall_mem_closedBall_exists_eq_isIntegralCurveOn
    (hf : ContDiffAt ‚Ñù 1 f x‚ÇÄ) (t‚ÇÄ : ‚Ñù) :
    ‚àÉ r > (0 : ‚Ñù), ‚àÉ Œµ > (0 : ‚Ñù), ‚àÄ x ‚àà closedBall x‚ÇÄ r, ‚àÉ Œ± : ‚Ñù ‚Üí E, Œ± t‚ÇÄ = x ‚àß
      IsIntegralCurveOn Œ± (fun _ ‚Ü¶ f) (Ioo (t‚ÇÄ - Œµ) (t‚ÇÄ + Œµ)) := by
  have ‚ü®Œµ, hŒµ, a, r, _, _, hr, hpl‚ü© := IsPicardLindelof.of_contDiffAt_one hf t‚ÇÄ
  refine ‚ü®r, hr, Œµ, hŒµ, fun x hx ‚Ü¶ ?_‚ü©
  have ‚ü®Œ±, hŒ±1, hŒ±2‚ü© := hpl.exists_eq_isIntegralCurveOn hx
  exact ‚ü®Œ±, hŒ±1, hŒ±2.mono Ioo_subset_Icc_self‚ü©

/-- If a vector field `f : E ‚Üí E` is continuously differentiable at `x‚ÇÄ : E`, then it admits an
integral curve `Œ± : ‚Ñù ‚Üí E` defined on an open interval, with initial condition `Œ± t‚ÇÄ = x‚ÇÄ`. -/
theorem exists_eq_isIntegralCurveAt
    (hf : ContDiffAt ‚Ñù 1 f x‚ÇÄ) (t‚ÇÄ : ‚Ñù) :
    ‚àÉ Œ± : ‚Ñù ‚Üí E, Œ± t‚ÇÄ = x‚ÇÄ ‚àß IsIntegralCurveAt Œ± (fun _ ‚Ü¶ f) t‚ÇÄ := by
  have ‚ü®_, hr, Œµ, hŒµ, H‚ü© := exists_forall_mem_closedBall_exists_eq_isIntegralCurveOn hf t‚ÇÄ
  have ‚ü®Œ±, hŒ±1, hŒ±2‚ü© := H x‚ÇÄ (mem_closedBall_self (le_of_lt hr))
  exact ‚ü®Œ±, hŒ±1, hŒ±2.isIntegralCurveAt (Ioo_mem_nhds (by linarith) (by linarith))‚ü©

open Classical in
/-- If a vector field `f : E ‚Üí E` is continuously differentiable at `x‚ÇÄ : E`, then it admits a flow
`Œ± : E ‚Üí ‚Ñù ‚Üí E` defined on an open domain, with initial condition `Œ± x t‚ÇÄ = x` for all `x` within
the domain. -/
theorem exists_eventually_isIntegralCurveAt
    (hf : ContDiffAt ‚Ñù 1 f x‚ÇÄ) (t‚ÇÄ : ‚Ñù) :
    ‚àÉ Œ± : E ‚Üí ‚Ñù ‚Üí E, ‚àÄ·∂† x in ùìù x‚ÇÄ,
      Œ± x t‚ÇÄ = x ‚àß IsIntegralCurveAt (Œ± x) (fun _ ‚Ü¶ f) t‚ÇÄ := by
  obtain ‚ü®r, hr, Œµ, hŒµ, H‚ü© := exists_forall_mem_closedBall_exists_eq_isIntegralCurveOn hf t‚ÇÄ
  choose Œ± hŒ± using H
  refine ‚ü®fun (x : E) ‚Ü¶ if hx : x ‚àà closedBall x‚ÇÄ r then Œ± x hx else 0, ?_‚ü©
  filter_upwards [closedBall_mem_nhds x‚ÇÄ hr] with x hx
  simp only [dif_pos hx]
  exact ‚ü®(hŒ± x hx).1,
    (hŒ± x hx).2.isIntegralCurveAt (Ioo_mem_nhds (by linarith) (by linarith))‚ü©

end ContDiffAt

/-! ## Uniqueness of solutions to ODEs -/

variable {E : Type*} [NormedAddCommGroup E] [NormedSpace ‚Ñù E]
  {v : ‚Ñù ‚Üí E ‚Üí E} {s : ‚Ñù ‚Üí Set E} {K : ‚Ñù‚â•0} {f g : ‚Ñù ‚Üí E} {a b t‚ÇÄ : ‚Ñù}

/-- There exists only one solution of an ODE $\dot x=v(t, x)$ in a set `s ‚äÜ ‚Ñù √ó E` with
a given initial value provided that the RHS is Lipschitz continuous in `x` within `s`,
and we consider only solutions included in `s`.

This version shows uniqueness in a closed interval `Icc a b`, where `a` is the initial time. -/
theorem IsIntegralCurveOn.eqOn_Icc_right
    (hv : ‚àÄ t ‚àà Ico a b, LipschitzOnWith K (v t) (s t))
    (hf : ContinuousOn f (Icc a b)) (hf' : IsIntegralCurveOn f v (Ico a b))
    (hfs : ‚àÄ t ‚àà Ico a b, f t ‚àà s t)
    (hg : ContinuousOn g (Icc a b)) (hg' : IsIntegralCurveOn g v (Ico a b))
    (hgs : ‚àÄ t ‚àà Ico a b, g t ‚àà s t) (ha : f a = g a) :
    EqOn f g (Icc a b) := fun t ht ‚Ü¶ by
  have := dist_le_of_trajectories_ODE_of_mem hv hf
    (fun t ht ‚Ü¶ (hf' t ht).mono_of_mem_nhdsWithin (Ico_mem_nhdsGE_of_mem ht)) hfs hg
    (fun t ht ‚Ü¶ (hg' t ht).mono_of_mem_nhdsWithin (Ico_mem_nhdsGE_of_mem ht)) hgs
    (dist_le_zero.2 ha) t ht
  rwa [zero_mul, dist_le_zero] at this

/-- A time-reversed version of `IsIntegralCurveOn.eqOn_Icc_right`. Uniqueness is shown in a
closed interval `Icc a b`, where `b` is the "initial" time. -/
theorem IsIntegralCurveOn.eqOn_Icc_left
    (hv : ‚àÄ t ‚àà Ioc a b, LipschitzOnWith K (v t) (s t))
    (hf : ContinuousOn f (Icc a b)) (hf' : IsIntegralCurveOn f v (Ioc a b))
    (hfs : ‚àÄ t ‚àà Ioc a b, f t ‚àà s t)
    (hg : ContinuousOn g (Icc a b)) (hg' : IsIntegralCurveOn g v (Ioc a b))
    (hgs : ‚àÄ t ‚àà Ioc a b, g t ‚àà s t) (hb : f b = g b) :
    EqOn f g (Icc a b) := by
  have hv' : ‚àÄ t ‚àà Ico (-b) (-a), LipschitzOnWith K (Neg.neg ‚àò (v (-t))) (s (-t)) := by
    intro t ht
    replace ht : -t ‚àà Ioc a b := by
      push _ ‚àà _ at ht ‚ä¢
      constructor <;> linarith
    rw [‚Üê one_mul K]
    exact LipschitzWith.id.neg.comp_lipschitzOnWith (hv _ ht)
  have hmt1 : MapsTo Neg.neg (Icc (-b) (-a)) (Icc a b) :=
    fun _ ht ‚Ü¶ ‚ü®le_neg.mp ht.2, neg_le.mp ht.1‚ü©
  have hmt2 : MapsTo Neg.neg (Ico (-b) (-a)) (Ioc a b) :=
    fun _ ht ‚Ü¶ ‚ü®lt_neg.mp ht.2, neg_le.mp ht.1‚ü©
  suffices EqOn (f ‚àò Neg.neg) (g ‚àò Neg.neg) (Icc (-b) (-a)) by
    rw [eqOn_comp_right_iff] at this
    convert this
    simp
  apply IsIntegralCurveOn.eqOn_Icc_right hv'
    (hf.comp continuousOn_neg hmt1) _ (fun _ ht ‚Ü¶ hfs _ (hmt2 ht))
    (hg.comp continuousOn_neg hmt1) _ (fun _ ht ‚Ü¶ hgs _ (hmt2 ht)) (by simp [hb])
  ¬∑ intro t ht
    convert HasFDerivWithinAt.comp_hasDerivWithinAt t (hf' (-t) (hmt2 ht))
      (hasDerivAt_neg t).hasDerivWithinAt hmt2
    simp
  ¬∑ intro t ht
    convert HasFDerivWithinAt.comp_hasDerivWithinAt t (hg' (-t) (hmt2 ht))
      (hasDerivAt_neg t).hasDerivWithinAt hmt2
    simp

/-- A version of `IsIntegralCurveOn.eqOn_Icc_right` for uniqueness in a closed interval whose
interior contains the initial time. -/
theorem IsIntegralCurveOn.eqOn_Icc
    (hv : ‚àÄ t ‚àà Ioo a b, LipschitzOnWith K (v t) (s t)) (ht : t‚ÇÄ ‚àà Ioo a b)
    (hf : ContinuousOn f (Icc a b)) (hf' : IsIntegralCurveOn f v (Ioo a b))
    (hfs : ‚àÄ t ‚àà Ioo a b, f t ‚àà s t)
    (hg : ContinuousOn g (Icc a b)) (hg' : IsIntegralCurveOn g v (Ioo a b))
    (hgs : ‚àÄ t ‚àà Ioo a b, g t ‚àà s t) (heq : f t‚ÇÄ = g t‚ÇÄ) :
    EqOn f g (Icc a b) := by
  rw [‚Üê Icc_union_Icc_eq_Icc (le_of_lt ht.1) (le_of_lt ht.2)]
  apply EqOn.union
  ¬∑ have hss : Ioc a t‚ÇÄ ‚äÜ Ioo a b := Ioc_subset_Ioo_right ht.2
    exact IsIntegralCurveOn.eqOn_Icc_left (fun t ht ‚Ü¶ hv t (hss ht))
      (hf.mono <| Icc_subset_Icc_right <| le_of_lt ht.2)
      (hf'.mono hss) (fun _ ht' ‚Ü¶ hfs _ (hss ht'))
      (hg.mono <| Icc_subset_Icc_right <| le_of_lt ht.2)
      (hg'.mono hss) (fun _ ht' ‚Ü¶ hgs _ (hss ht')) heq
  ¬∑ have hss : Ico t‚ÇÄ b ‚äÜ Ioo a b := Ico_subset_Ioo_left ht.1
    exact IsIntegralCurveOn.eqOn_Icc_right (fun t ht ‚Ü¶ hv t (hss ht))
      (hf.mono <| Icc_subset_Icc_left <| le_of_lt ht.1)
      (hf'.mono hss) (fun _ ht' ‚Ü¶ hfs _ (hss ht'))
      (hg.mono <| Icc_subset_Icc_left <| le_of_lt ht.1)
      (hg'.mono hss) (fun _ ht' ‚Ü¶ hgs _ (hss ht')) heq

/-- A version of `IsIntegralCurveOn.eqOn_Icc` for uniqueness in an open interval. -/
theorem IsIntegralCurveOn.eqOn_Ioo
    (hv : ‚àÄ t ‚àà Ioo a b, LipschitzOnWith K (v t) (s t)) (ht : t‚ÇÄ ‚àà Ioo a b)
    (hf : IsIntegralCurveOn f v (Ioo a b)) (hfs : ‚àÄ t ‚àà Ioo a b, f t ‚àà s t)
    (hg : IsIntegralCurveOn g v (Ioo a b)) (hgs : ‚àÄ t ‚àà Ioo a b, g t ‚àà s t)
    (heq : f t‚ÇÄ = g t‚ÇÄ) :
    EqOn f g (Ioo a b) := by
  intro t' ht'
  rcases lt_or_ge t' t‚ÇÄ with (h | h)
  ¬∑ have hss : Icc t' t‚ÇÄ ‚äÜ Ioo a b :=
      fun _ ht'' ‚Ü¶ ‚ü®lt_of_lt_of_le ht'.1 ht''.1, lt_of_le_of_lt ht''.2 ht.2‚ü©
    exact IsIntegralCurveOn.eqOn_Icc_left
      (fun t'' ht'' ‚Ü¶ hv t'' ((Ioc_subset_Icc_self.trans hss) ht''))
      (hf.continuousOn.mono hss)
      (hf.mono (Ioc_subset_Icc_self.trans hss))
      (fun _ ht'' ‚Ü¶ hfs _ (hss (Ioc_subset_Icc_self ht'')))
      (hg.continuousOn.mono hss)
      (hg.mono (Ioc_subset_Icc_self.trans hss))
      (fun _ ht'' ‚Ü¶ hgs _ (hss (Ioc_subset_Icc_self ht''))) heq
      ‚ü®le_rfl, le_of_lt h‚ü©
  ¬∑ have hss : Icc t‚ÇÄ t' ‚äÜ Ioo a b :=
      fun _ ht'' ‚Ü¶ ‚ü®lt_of_lt_of_le ht.1 ht''.1, lt_of_le_of_lt ht''.2 ht'.2‚ü©
    exact IsIntegralCurveOn.eqOn_Icc_right
      (fun t'' ht'' ‚Ü¶ hv t'' ((Ico_subset_Icc_self.trans hss) ht''))
      (hf.continuousOn.mono hss)
      (hf.mono (Ico_subset_Icc_self.trans hss))
      (fun _ ht'' ‚Ü¶ hfs _ (hss (Ico_subset_Icc_self ht'')))
      (hg.continuousOn.mono hss)
      (hg.mono (Ico_subset_Icc_self.trans hss))
      (fun _ ht'' ‚Ü¶ hgs _ (hss (Ico_subset_Icc_self ht''))) heq
      ‚ü®h, le_rfl‚ü©

/-- Local uniqueness of ODE solutions. -/
theorem IsIntegralCurveAt.eventuallyEq
    (hv : ‚àÄ·∂† t in ùìù t‚ÇÄ, LipschitzOnWith K (v t) (s t))
    (hf : IsIntegralCurveAt f v t‚ÇÄ) (hfs : ‚àÄ·∂† t in ùìù t‚ÇÄ, f t ‚àà s t)
    (hg : IsIntegralCurveAt g v t‚ÇÄ) (hgs : ‚àÄ·∂† t in ùìù t‚ÇÄ, g t ‚àà s t)
    (heq : f t‚ÇÄ = g t‚ÇÄ) : f =·∂†[ùìù t‚ÇÄ] g := by
  obtain ‚ü®Œµ, hŒµ, h‚ü© := eventually_nhds_iff_ball.mp (hv.and ((hf.and hfs).and (hg.and hgs)))
  rw [Filter.eventuallyEq_iff_exists_mem]
  refine ‚ü®ball t‚ÇÄ Œµ, ball_mem_nhds _ hŒµ, ?_‚ü©
  simp_rw [Real.ball_eq_Ioo] at *
  exact IsIntegralCurveOn.eqOn_Ioo (fun _ ht ‚Ü¶ (h _ ht).1)
    (Real.ball_eq_Ioo t‚ÇÄ Œµ ‚ñ∏ mem_ball_self hŒµ)
    (fun _ ht ‚Ü¶ (h _ ht).2.1.1.hasDerivWithinAt) (fun _ ht ‚Ü¶ (h _ ht).2.1.2)
    (fun _ ht ‚Ü¶ (h _ ht).2.2.1.hasDerivWithinAt) (fun _ ht ‚Ü¶ (h _ ht).2.2.2) heq

/-- There exists only one global solution to an ODE $\dot x=v(t, x)$ with a given initial value
provided that the RHS is Lipschitz continuous. -/
theorem IsIntegralCurve.eq
    (hv : ‚àÄ t, LipschitzOnWith K (v t) (s t))
    (hf : IsIntegralCurve f v) (hfs : ‚àÄ t, f t ‚àà s t)
    (hg : IsIntegralCurve g v) (hgs : ‚àÄ t, g t ‚àà s t)
    (heq : f t‚ÇÄ = g t‚ÇÄ) : f = g := by
  ext t
  obtain ‚ü®A, B, Ht, Ht‚ÇÄ‚ü© : ‚àÉ A B, t ‚àà Set.Ioo A B ‚àß t‚ÇÄ ‚àà Set.Ioo A B := by
    use (min (-|t|) (-|t‚ÇÄ|) - 1), (max |t| |t‚ÇÄ| + 1)
    rw [Set.mem_Ioo, Set.mem_Ioo]
    refine ‚ü®‚ü®?_, ?_‚ü©, ?_, ?_‚ü©
    ¬∑ exact sub_one_lt _ |>.trans_le <| min_le_left _ _ |>.trans <| neg_abs_le t
    ¬∑ exact le_abs_self _ |>.trans_lt <| le_max_left _ _ |>.trans_lt <| lt_add_one _
    ¬∑ exact sub_one_lt _ |>.trans_le <| min_le_right _ _ |>.trans <| neg_abs_le t‚ÇÄ
    ¬∑ exact le_abs_self _ |>.trans_lt <| le_max_right _ _ |>.trans_lt <| lt_add_one _
  exact IsIntegralCurveOn.eqOn_Ioo
    (fun t _ => hv t) Ht‚ÇÄ (hf.isIntegralCurveOn _) (fun t _ => hfs t)
    (hg.isIntegralCurveOn _) (fun t _ => hgs t) heq Ht
